Tart Ultraviolet Kitten

medium

# The respectedGameType is not initialized leading to unsactioned game types being respected

## Summary

The `respectedGameType` registered in the OptimismPortal2 is not initialized correctly and therefore may point to an incorrect value. Therefore, an unsanctioned game type could be used to prove malicious OutputRoots, compromising the safety of user funds.

## Vulnerability Detail

The `respectedGameType` is a critical state variable in the Portal, which is checked before processing a withdrawal - the proof game type must be respected:

```solidity
// The game type of the dispute game must be the respected game type. This was also checked in
// `proveWithdrawalTransaction`, but we check it again in case the respected game type has changed since
// the withdrawal was proven.
require(disputeGameProxy.gameType().raw() == respectedGameType.raw(), "OptimismPortal: invalid game type");
```

The OptimismPortal2 is composed of an implementation contract and a proxy contract. The constructor is run in the implementation context while the `initialize()` function is ran in the proxy's context.

The constructor sets three parameters:
```solidity
PROOF_MATURITY_DELAY_SECONDS = _proofMaturityDelaySeconds;
DISPUTE_GAME_FINALITY_DELAY_SECONDS = _disputeGameFinalityDelaySeconds;
respectedGameType = _initialRespectedGameType;
```

Note  that `respectedGameType` is different from the other two parameters, because it is not defined as `immutable`. 
`uint256 internal immutable DISPUTE_GAME_FINALITY_DELAY_SECONDS;`
`GameType public respectedGameType;`

Immutable variables are hard coded in the contract's bytecode at construction time, therefore they can be initialized at the implementation's constructor.
However, state variables must be initialized at the proxy's `initialize()`, because they are fetched from its storage using SLOADs at runtime.

As a result, the `respectedGameType` at the Proxy will be zero to start with. instead of the desired `_initialRespectedGameType`.
Note that GameType 0 is normal Cannon, where the safer game to start with when rolling out the Fault Proof mechanism is Permissioned Cannon, or other variants which provide safety wheels. 

## Impact

The issue leads to an unexpected game type being respected. Depending on circumstances it certainly leads to increased risk of unsanctioned games being played and respected. Additionally, if and when the developers realize the issue and correct the game type, all games played until that point are invalidated due to having lower `createdAt` than the set `respectedGameTypeUpdatedAt`.

## Code Snippet

```solidity
constructor(
    uint256 _proofMaturityDelaySeconds,
    uint256 _disputeGameFinalityDelaySeconds,
    GameType _initialRespectedGameType
) {
    PROOF_MATURITY_DELAY_SECONDS = _proofMaturityDelaySeconds;
    DISPUTE_GAME_FINALITY_DELAY_SECONDS = _disputeGameFinalityDelaySeconds;
    respectedGameType = _initialRespectedGameType;
    initialize({
        _disputeGameFactory: DisputeGameFactory(address(0)),
        _systemConfig: SystemConfig(address(0)),
        _superchainConfig: SuperchainConfig(address(0))
    });
}
```

## Tool used

Manual Review

## Recommendation

Move the initilization of `respectedGameType` to the `initialize()` function.
