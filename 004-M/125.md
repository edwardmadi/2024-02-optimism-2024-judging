Hot Stone Peacock

medium

# `calldatasize()` check in `FaultDisputeGame.initialize()` can be bypassed when `_extraData` has less bytes

## Summary

For L2 block numbers that overlap with CWIA bytes in `FaultDisputeGame`, attackers can specify `_extraData` with less bytes to deploy multiple dispute games for the same output proposal.

## Vulnerability Detail

When new dispute games are created, `FaultDisputeGame.initialize()` checks that `calldatasize()` is not greater than `0x66`:

[FaultDisputeGame.sol#L541-L552](https://github.com/sherlock-audit/2024-02-optimism-2024/blob/f216b0d3ad08c1a0ead557ea74691aaefd5fd489/optimism/packages/contracts-bedrock/src/dispute/FaultDisputeGame.sol#L541-L552)

```solidity
        // Revert if the calldata size is too large, which signals that the `extraData` contains more than expected.
        // This is to prevent adding extra bytes to the `extraData` that result in a different game UUID in the factory,
        // but are not used by the game, which would allow for multiple dispute games for the same output proposal to
        // be created.
        // Expected length: 0x66 (0x04 selector + 0x20 root claim + 0x20 l1 head + 0x20 extraData + 0x02 CWIA bytes)
        assembly {
            if gt(calldatasize(), 0x66) {
                // Store the selector for `ExtraDataTooLong()` & revert
                mstore(0x00, 0xc407e025)
                revert(0x1C, 0x04)
            }
        }
```

As described in the comment above, this check ensures users cannot call `DisputeGameFactory.create()` with extra bytes appended to `_extraData`. This prevents attackers from creating multiple dispute games with the same output proposal (i.e. `_gameType`, `_rootClaim` and `_extraData`), which is enforced by the following check:

[DisputeGameFactory.sol#L109-L113](https://github.com/sherlock-audit/2024-02-optimism-2024/blob/f216b0d3ad08c1a0ead557ea74691aaefd5fd489/optimism/packages/contracts-bedrock/src/dispute/DisputeGameFactory.sol#L109-L113)

```solidity
        // Compute the unique identifier for the dispute game.
        Hash uuid = getGameUUID(_gameType, _rootClaim, _extraData);

        // If a dispute game with the same UUID already exists, revert.
        if (GameId.unwrap(_disputeGames[uuid]) != bytes32(0)) revert GameAlreadyExists(uuid);
```

However, the `gt(calldatasize(), 0x66)` check does not account for the scenario where `_extraData` has less than 32 bytes. Note that in `FaultDisputeGame`, the bytes in `_extraData` are [decoded to `l2BlockNumber()`](https://github.com/sherlock-audit/2024-02-optimism-2024/blob/f216b0d3ad08c1a0ead557ea74691aaefd5fd489/optimism/packages/contracts-bedrock/src/dispute/FaultDisputeGame.sol#L372-L374). 

Therefore, when the ending bytes of `l2BlockNumber` overlap with the 2 CWIA bytes appended to `FaultDisputeGame`'s calldata, an attacker can remove the last few bytes from `_extraData` to obtain a different `uuid`. However, when `l2BlockNumber()` is decoded in `FaultDisputeGame`, the result will still be the same.

For example:
- Assume that `l2BlockNumber` is a multiple of 256, such as `118286080`, which is `0x70ce700` in hex.
- The last two CWIA bytes is `0x0066`.
- Deploy one dispute game with `_extraData = abi.encode(uint256(0x70ce700))`.
- Deploy another dispute game with `_extraData = abi.encodePacked(uint248(0x70ce7))`:
  - i.e. `_extraData` has a length of 31 and ends with `070ce7`. The last byte, which was `0x00`, is removed.
  - Since `_extraData` is different, the `_disputeGames[uuid] != bytes32(0)` check passes.
  - When `l2BlockNumber()` is called, it takes 31 bytes from `_extraData` and the first CWIA byte, which is `0x00`.
  - This results in `l2BlockNumber()` being decoded as `0x70ce700`, which is the exact same as the first dispute game.

As seen from the example above, an attacker can bypass both checks to deploy two dispute games for the exact same output proposal. Note that depending on how many CWIA bytes overlap with `l2BlockNumber()`, it could be possible to deploy more than two dispute games.

The following PoC proves that the example above is possible:

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.15;

import { FaultDisputeGame_Init } from "test/dispute/FaultDisputeGame.t.sol";
import { Claim } from "src/libraries/DisputeTypes.sol";
import { FaultDisputeGame } from "src/dispute/FaultDisputeGame.sol";

contract FaultDisputeGame_Test is FaultDisputeGame_Init {
    function test_create_multiple_games_with_same_outputRoot() public {
        // Create FaultDisputeGame implementation
        Claim dummyClaim = Claim.wrap(bytes32(0));
        super.init(dummyClaim, dummyClaim, 0x10);

        // Output proposal has the following arguments:
        Claim _rootClaim = Claim.wrap(bytes32(hex"deadbeef"));
        uint256 l2BlockNumber = 118286080; // 0x70ce700
        bytes memory _extraData = abi.encode(l2BlockNumber);

        // Deploy first FDG regularly
        FaultDisputeGame gameProxy1 = FaultDisputeGame(payable(address(disputeGameFactory.create(
            GAME_TYPE, 
            _rootClaim, 
            _extraData            
        ))));

        // Deploy second FDG, but remove the last byte from _extraData
        FaultDisputeGame gameProxy2 = FaultDisputeGame(payable(address(disputeGameFactory.create(
            GAME_TYPE, 
            _rootClaim, 
            bytes.concat(bytes31(_extraData))
        ))));

        // Both FDGs have the same output root and l2BlockNumber
        assertEq(gameProxy1.rootClaim().raw(), gameProxy2.rootClaim().raw());
        assertEq(gameProxy1.l2BlockNumber(), l2BlockNumber);
        assertEq(gameProxy2.l2BlockNumber(), l2BlockNumber);
    }
}
```

## Impact

As it is possible to deploy multiple dispute games for the same output proposal, honest challengers will have to play multiple dispute games for the same output proposal, wasting resources:
- Challengers' ETH is locked up to create claims in both dispute games. Note that it can be locked up to 14 days (7 days for `GAME_DURATION` and 7 days for `DelayedWETH.DELAY_SECONDS`).
- Challengers have to generate proofs for multiple games, instead of just one.

## Code Snippet

https://github.com/sherlock-audit/2024-02-optimism-2024/blob/f216b0d3ad08c1a0ead557ea74691aaefd5fd489/optimism/packages/contracts-bedrock/src/dispute/FaultDisputeGame.sol#L541-L552

https://github.com/sherlock-audit/2024-02-optimism-2024/blob/f216b0d3ad08c1a0ead557ea74691aaefd5fd489/optimism/packages/contracts-bedrock/src/dispute/DisputeGameFactory.sol#L109-L113

## Tool used

Manual Review

## Recommendation

In `FaultDisputeGame.initialize()`, check that `calldatasize()` is exactly `0x66`:

```diff
        assembly {
-           if gt(calldatasize(), 0x66) {
+           if iszero(eq(calldatasize(), 0x66)) {
                // Store the selector for `ExtraDataTooLong()` & revert
                mstore(0x00, 0xc407e025)
                revert(0x1C, 0x04)
            }
        }

```
