Delightful Gingerbread Carp

medium

# Multiple dispute games for the same output proposal can be created

## Summary

The system is designed to prevent creating two dispute games for the same output proposal.

But every 256 L2 blocks, a user can deploy two dispute games for the same output proposal.


## Vulnerability Detail

During the deployment of a dispute game, the `DisputeGameFactory` [calculates an unique Game UUID](https://github.com/sherlock-audit/2024-02-optimism-2024/blob/main/optimism/packages/contracts-bedrock/src/dispute/DisputeGameFactory.sol#L110) from a game type, a root claim and an `_extraData` argument.
In case of the `FaultDisputeGame`, the `_extraData` must be the L2 block number corresponding to the root claim.

During the initialization of the `FaultDisputeGame`, a check is made to ensure that the `_extraData` is not greater than 32 bytes. [A comment attached to this verification](https://github.com/sherlock-audit/2024-02-optimism-2024/blob/main/optimism/packages/contracts-bedrock/src/dispute/FaultDisputeGame.sol#L541-L545) indicates the following:
> Revert if the calldata size is too large, which signals that the `extraData` contains more than expected.
> This is to prevent adding extra bytes to the `extraData` that result in a different game UUID in the factory,
> but are not used by the game, which would allow for multiple dispute games for the same output proposal to
> be created.
> 
> Expected length: 0x66 (0x04 selector + 0x20 root claim + 0x20 l1 head + 0x20 extraData + 0x02 CWIA bytes)

*Note: The `CWIA bytes` encodes the size of the additional call data on 2 bytes.*

However, when `l2BlockNumber` hexadecimal representation ends with `0x00`, the check can be bypassed by setting an `_extraData` of length 31 instead of 32. This comes from the fact that the 32nd byte will be read from the first `CWIA` byte (`0x0061` in this case, the `0x00` part will be interpreted as the 32nd byte of `_extraData`).

An l2BlockNumber ending with `0x00` as last byte is achieved every 256 blocks.

## Impact

Bypass of an implemented security mechanism, leading to deployment of duplicated games.

Reported as MEDIUM because the protocol is designed to forbid this specific behavior.

## Code Snippet

[During a game creation in `DisputeGameFactory.create`](https://github.com/sherlock-audit/2024-02-optimism-2024/blob/main/optimism/packages/contracts-bedrock/src/dispute/DisputeGameFactory.sol#L106-L122), the game is created and initialized, and its unique UUID is calculated:

*Note: Read the `@POC` comment for explanations.*

```solidity
    /// @inheritdoc IDisputeGameFactory
    function create(
        GameType _gameType,
        Claim _rootClaim,
        bytes calldata _extraData
    )
        external
        payable
        returns (IDisputeGame proxy_)
    {
        // @POC: REDACTED FOR READABILITY

        // Clone the implementation contract and initialize it with the given parameters.
        proxy_ = IDisputeGame(address(impl).clone(abi.encodePacked(_rootClaim, parentHash, _extraData))); // @POC: `_extraData` is included as third argument, its offset will be `0x40`
        proxy_.initialize{ value: msg.value }(); // @POC: arguments are not passed here, but the Clone will include them when calling the implementation

        // Compute the unique identifier for the dispute game.
        Hash uuid = getGameUUID(_gameType, _rootClaim, _extraData);

        // If a dispute game with the same UUID already exists, revert.
        if (GameId.unwrap(_disputeGames[uuid]) != bytes32(0)) revert GameAlreadyExists(uuid);

        // Pack the game ID.
        GameId id = LibGameId.pack(_gameType, Timestamp.wrap(uint64(block.timestamp)), proxy_);

        // Store the dispute game id in the mapping & emit the `DisputeGameCreated` event.
        _disputeGames[uuid] = id;
        _disputeGameList.push(id);
        emit DisputeGameCreated(address(proxy_), _gameType, _rootClaim);
    }

    function getGameUUID(
        GameType _gameType,
        Claim _rootClaim,
        bytes calldata _extraData
    )
        public
        pure
        returns (Hash uuid_)
    {
        uuid_ = Hash.wrap(keccak256(abi.encode(_gameType, _rootClaim, _extraData))); // @POC: `_extraData` includes the size of its content
    }
```

As we can see, data is passed when the Clone Proxy is created. This special clone will store the `_rootClaim, parentHash, _extraData` arguments and it will include them when delegatecalling the implementation.

Then, when `initialize` is called, the proxy will automatically append these specific arguments to the calldata.

[In the game initialization](https://github.com/sherlock-audit/2024-02-optimism-2024/blob/main/optimism/packages/contracts-bedrock/src/dispute/FaultDisputeGame.sol#L541-L552), the calldata length is **checked to not be greater** than `0x66`.

The check is not a strict equality, it passes if `length <= 0x66`. This lead to possible bypass of the mechanism.

*Note that args are passed through calldata without being defined in the `initialize` function.*

```solidity
    function initialize() public payable virtual {
        // @POC: REDACTED

        // Do not allow the game to be initialized if the root claim corresponds to a block at or before the
        // configured starting block number.
        if (l2BlockNumber() <= rootBlockNumber) revert UnexpectedRootClaim(rootClaim());

        // Revert if the calldata size is too large, which signals that the `extraData` contains more than expected.
        // This is to prevent adding extra bytes to the `extraData` that result in a different game UUID in the factory,
        // but are not used by the game, which would allow for multiple dispute games for the same output proposal to
        // be created.
        // Expected length: 0x66 (0x04 selector + 0x20 root claim + 0x20 l1 head + 0x20 extraData + 0x02 CWIA bytes)
        assembly {
            if gt(calldatasize(), 0x66) {
                // Store the selector for `ExtraDataTooLong()` & revert
                mstore(0x00, 0xc407e025)
                revert(0x1C, 0x04)
            }
        }

        // @POC: REDACTED
    }
```

The [`FaultDisputeGame.l2BlockNumber()` function](https://github.com/sherlock-audit/2024-02-optimism-2024/blob/main/optimism/packages/contracts-bedrock/src/dispute/FaultDisputeGame.sol#L372) will use the [`_getArgUint256` function](https://github.com/sherlock-audit/2024-02-optimism-2024/blob/main/optimism/packages/contracts-bedrock/src/libraries/Clone.sol#L28-L33) to read 32 bytes as a uint256 at offset `0x40` of calldata.

```solidity
    function l2BlockNumber() public pure returns (uint256 l2BlockNumber_) {
        l2BlockNumber_ = _getArgUint256(0x40);
    }

    function _getArgUint256(uint256 argOffset) internal pure returns (uint256 arg) {
        uint256 offset = _getImmutableArgsOffset();
        assembly {
            arg := calldataload(add(offset, argOffset))
        }
    }
```

Knowing all this, it is possible to exploit the security mechanism that checks if the size is not greater than `0x66`, by setting a size lower than `0x66`.

Take `l2BlockNumber = 1_000_192`. Its uint256 representation as hexadecimal is:
```text
0x00000000000000000000000000000000000000000000000000000000000F4300
```

`_extraData` being of type `bytes memory`, it encodes this number by also including its size (32-byte size). This (size + content) will be used in `DisputeGameFactory.getGameUUID` to generate the unique UUID.

As the last byte of `l2BlockNumber` is `0x00` and reading the first byte of `CWIA` returns `0x00`, it is possible to encode it in `_extraData` with a size of 31 by deleting the last byte.

```text
0x00000000000000000000000000000000000000000000000000000000000F43
```

This encoded payload will:
-  give a different UUID when used with `DisputeGameFactory.getGameUUID`
-  be read as 32 bytes instead of 31 in `FaultDisputeGame.l2BlockNumber()`. As one additional empty byte is read, it will result in `1_000_192`.

The provided PoC shows such an exploit.

*Note: The behavior of reading CWIA bytes could be further exploited to read 2 bytes instead of 1.*

## Proof of Concept

The following unit test can be added to `test/dispute/FaultDisputeGame.t.sol`:

```solidity
    function test_bypassSecondGameDeployedWithSameArgs() public {
        // @POC: Create a game at block 1_000_192 with claimRoot 12345
        FaultDisputeGame game = FaultDisputeGame(payable(address(disputeGameFactory.create(GameType.wrap(uint8(0)), Claim.wrap(bytes32(uint256(12345))), abi.encode(0x00000000000000000000000000000000000000000000000000000000000F4300)))));

        // @POC: Create a second game at block 1_000_192 with same rootClaim SHOULD NOT be possible
        // @POC: Note that here, only 31 bytes are passed as `l2BlockNumber`.
        // @POC: The `l2BlockNumber()` function will read 32 bytes even if `_extraData` is 31-byte long.
        // @POC: As following bytes are `0x00`, 
        FaultDisputeGame secondGame = FaultDisputeGame(payable(address(disputeGameFactory.create(GameType.wrap(uint8(0)), Claim.wrap(bytes32(uint256(12345))), abi.encodePacked(bytes31(0x00000000000000000000000000000000000000000000000000000000000F43))))));

        assertEq(game.gameType().raw(), secondGame.gameType().raw(), "Different Root claim");   // @POC: Same GameType
        assertEq(game.rootClaim().raw(), secondGame.rootClaim().raw(), "Different Root claim"); // @POC: Same rootClaim
        assertEq(game.l2BlockNumber(), secondGame.l2BlockNumber(), "Different l2BlockNumber");  // @POC: Same l2BlockNumber
    }
```

For more information, you can import `forge-std/console2.sol` in `FaultDisputeGame.sol` and print the calldata size in `initialize`:

```solidity
        // Revert if the calldata size is too large, which signals that the `extraData` contains more than expected.
        // This is to prevent adding extra bytes to the `extraData` that result in a different game UUID in the factory,
        // but are not used by the game, which would allow for multiple dispute games for the same output proposal to
        // be created.
        // Expected length: 0x66 (0x04 selector + 0x20 root claim + 0x20 l1 head + 0x20 extraData + 0x02 CWIA bytes)
        uint256 pocSize;
        assembly {
            pocSize := calldatasize()
            if gt(calldatasize(), 0x66) {
                // Store the selector for `ExtraDataTooLong()` & revert
                mstore(0x00, 0xc407e025)
                revert(0x1C, 0x04)
            }
        }
        console2.log("Calldatasize =", pocSize);
```

Used with the previous PoC, the printed output shows the two games deployment:
```text
  Calldatasize 102
  Calldatasize 101
```

## Tool used

Manual Review

## Recommendation

According to comment at [FaultDisputeGame.sol#L545](https://github.com/sherlock-audit/2024-02-optimism-2024/blob/main/optimism/packages/contracts-bedrock/src/dispute/FaultDisputeGame.sol#L545), the expected length is `0x66`.

Then, consider using a strict equality to ensure that the calldata size is `0x66`.

The following patch implements such a fix:

```diff
diff --git a/optimism/packages/contracts-bedrock/src/dispute/FaultDisputeGame.sol b/optimism/packages/contracts-bedrock/src/dispute/FaultDisputeGame.sol
index 3d32a19..12c79d3 100644
--- a/optimism/packages/contracts-bedrock/src/dispute/FaultDisputeGame.sol
+++ b/optimism/packages/contracts-bedrock/src/dispute/FaultDisputeGame.sol
@@ -544,7 +544,7 @@ contract FaultDisputeGame is IFaultDisputeGame, Clone, ISemver {
         // be created.
         // Expected length: 0x66 (0x04 selector + 0x20 root claim + 0x20 l1 head + 0x20 extraData + 0x02 CWIA bytes)
         assembly {
-            if gt(calldatasize(), 0x66) {
+            if not(eq(calldatasize(), 0x66)) {
                 // Store the selector for `ExtraDataTooLong()` & revert
                 mstore(0x00, 0xc407e025)
                 revert(0x1C, 0x04)

```

*Note: The provided patch can be applied with `git apply`.*