Itchy Tan Orangutan

high

# extraData might not equal _extraData

## Summary
`FaultDisputeGame.extraData()` might not equal to the `_extraData` parameter that's passed into factory during it's creation.


## Vulnerability Detail
`FaultDisputeGame.initialize` would require calldatasize to be less than or equal to 66. According to the comment `Expected length: 0x66 (0x04 selector + 0x20 root claim + 0x20 l1 head + 0x20 extraData + 0x02 CWIA bytes)`. But it should be noted that extraData is dynamic bytes. It could be 32 bytes as expected or it could be less and the require statement would pass. The issue is that when it's time to get extraData or l2BlockNumber, it reads exactly 32 bytes from arguments. To reach the 32 bytes, CWIA would right pad the provided _extraData.


## PoC
The following is a PoC that could be added to [FaultDisputeGame.t.sol](https://github.com/sherlock-audit/2024-02-optimism-2024/blob/main/optimism/packages/contracts-bedrock/test/dispute/FaultDisputeGame.t.sol). It shows that we could create 2 games with exactly the same gameType, rootClaim, extraData, and l2BlockNumber.

```solidity
contract FaultDisputeGame_Extradata_Test is DisputeGameFactory_Init {
    /// @dev The type of the game being tested.
    GameType internal constant GAME_TYPE = GameType.wrap(0);

    /// @dev The implementation of the game.
    FaultDisputeGame internal gameImpl;

    /// @dev The `Clone` proxies of the game.
    FaultDisputeGame internal gameProxy1;
    FaultDisputeGame internal gameProxy2;

    /// @dev The extra data passed to the game for initialization.
    bytes internal extraData1;
    bytes internal extraData2;

    /// @dev The root claim of the game.
    Claim internal constant ROOT_CLAIM = Claim.wrap(bytes32((uint256(1) << 248) | uint256(10)));
    /// @dev The preimage of the absolute prestate claim
    bytes internal absolutePrestateData;
    /// @dev The absolute prestate of the trace.
    Claim internal absolutePrestate;

    function setUp() public override {
        absolutePrestateData = abi.encode(0);
        absolutePrestate = _changeClaimStatus(Claim.wrap(keccak256(absolutePrestateData)), VMStatuses.UNFINISHED);

        super.setUp();
        
        // Set the time to a realistic date.
        vm.warp(1690906994);

        AlphabetVM _vm = new AlphabetVM(absolutePrestate, new PreimageOracle(0, 0));

        // Deploy an implementation of the fault game
        gameImpl = new FaultDisputeGame({
            _gameType: GAME_TYPE,
            _absolutePrestate: absolutePrestate,
            _maxGameDepth: 2 ** 3,
            _splitDepth: 2 ** 2,
            _gameDuration: Duration.wrap(7 days),
            _vm: _vm,
            _weth: delayedWeth,
            _anchorStateRegistry: anchorStateRegistry,
            _l2ChainId: 10
        });

        // Register the game implementation with the factory.
        disputeGameFactory.setImplementation(GAME_TYPE, gameImpl);
    }

    function test_extraData_collision() public {
        uint l2BlockNumber = 118278144;
        extraData1 = abi.encodePacked(uint256(l2BlockNumber));
        extraData2 = abi.encodePacked(uint248(l2BlockNumber / 256));

        gameProxy1 = FaultDisputeGame(payable(address(disputeGameFactory.create(GAME_TYPE, ROOT_CLAIM, extraData1))));
        gameProxy2 = FaultDisputeGame(payable(address(disputeGameFactory.create(GAME_TYPE, ROOT_CLAIM, extraData2))));

        // Check gameData
        assertEq(gameProxy1.gameType().raw(), gameProxy2.gameType().raw());
        assertEq(gameProxy1.rootClaim().raw(), gameProxy2.rootClaim().raw());
        assertEq(gameProxy1.extraData(), gameProxy2.extraData());
        assertEq(gameProxy1.l2BlockNumber(), gameProxy2.l2BlockNumber());
    }
}
```


## Impact
The project is expected to maintain a number of invariants which is broken
* There's only a single instance of FaultDisputeGame with the same gameType, rootClaim, extraData and l2BlockNumber
* extraData in Factory should be equal to extraData in Game
* extraData provided by Game could be used to calculates it's UUID in factory 


## Code Snippet
https://github.com/sherlock-audit/2024-02-optimism-2024/blob/main/optimism/packages/contracts-bedrock/src/dispute/FaultDisputeGame.sol#L545-L552
https://github.com/sherlock-audit/2024-02-optimism-2024/blob/main/optimism/packages/contracts-bedrock/src/dispute/FaultDisputeGame.sol#L488
https://github.com/sherlock-audit/2024-02-optimism-2024/blob/main/optimism/packages/contracts-bedrock/src/dispute/FaultDisputeGame.sol#L373
https://github.com/sherlock-audit/2024-02-optimism-2024/blob/main/optimism/packages/contracts-bedrock/src/dispute/DisputeGameFactory.sol#L106-L178


## Tool used

Manual Review

## Recommendation
require that the provided _extraData is exactly 32 bytes.
