Bubbly Fern Salamander

medium

# Players can still withdraw their bond if the game is resolved wrongly

## Summary

When the game is resolved wrongly, the guardian has `DISPUTE_GAME_FINALITY_DELAY_SECONDS` to dispute any issue and block users from withdrawing the bond by letting the owner withdraw all the bonds from the DelayedWETH contract. The problem is players can still withdraw their bond.

## Vulnerability Detail

There will be many FaultDisputeGame contracts that points to one DelayedWETH contract because the FaultDisputeGame is created as a clone. When players partake in the FaultDisputeGame, through `move()`, they will have to deposit the bond into the DelayedWETH contract.

```solidity
FaultDisputeGame.sol
        function move() 
        ...
        // Deposit the bond.
        WETH.deposit{ value: msg.value }();
```

This will call the DelayedWETH contract which will call the `deposit()` function in the WETH98 contract. Note the message sender is the instance of the FaultDisputeGame.

```solidity
    /// @inheritdoc IWETH
    function deposit() public payable {
        balanceOf[msg.sender] += msg.value;
        emit Deposit(msg.sender, msg.value);
    }
```

When it is time for the players to get back / earn their bond, `distributeBond()` will be called to start the unlocking process.

```solidity
    function _distributeBond(address _recipient, ClaimData storage _bonded) internal {
        // Set all bits in the bond value to indicate that the bond has been paid out.
        uint256 bond = _bonded.bond;
        if (bond == CLAIMED_BOND_FLAG) revert ClaimAlreadyResolved();
        _bonded.bond = CLAIMED_BOND_FLAG;

        // Increase the recipient's credit.
        credit[_recipient] += bond;

        // Unlock the bond.
        WETH.unlock(_recipient, bond);
    }
```

```solidity
DelayedWETH.sol
   /// @inheritdoc IDelayedWETH
    function unlock(address _guy, uint256 _wad) external {
        // Note that the unlock function can be called by any address, but the actual unlocking capability still only
        // gives the msg.sender the ability to withdraw from the account. As long as the unlock and withdraw functions
        // are called with the proper recipient addresses, this will be safe. Could be made safer by having external
        // accounts execute withdrawals themselves but that would have added extra complexity and made DelayedWETH a
        // leaky abstraction, so we chose this instead.
        WithdrawalRequest storage wd = withdrawals[msg.sender][_guy];
        wd.timestamp = block.timestamp;
        wd.amount += _wad;
    }
```

After the unlock process is over, the player can call `claimCredit()` to claim their bond.

```solidity
    function claimCredit(address _recipient) external {
        // Remove the credit from the recipient prior to performing the external call.
        uint256 recipientCredit = credit[_recipient];
        credit[_recipient] = 0;

        // Revert if the recipient has no credit to claim.
        if (recipientCredit == 0) {
            revert NoCreditToClaim();
        }

        // Try to withdraw the WETH amount so it can be used here.
        WETH.withdraw(_recipient, recipientCredit);

        // Transfer the credit to the recipient.
        (bool success,) = _recipient.call{ value: recipientCredit }(hex"");
        if (!success) revert BondTransferFailed();
    }
```

which will call `WETH.withdraw()` which calls `super.withdraw()` and deducts the balance of the `msg.sender` (the FaultDisputeGame)

```solidity
    /// @inheritdoc IDelayedWETH
    function withdraw(address _guy, uint256 _wad) public {
        require(!config.paused(), "DelayedWETH: contract is paused");
        WithdrawalRequest storage wd = withdrawals[msg.sender][_guy];
        require(wd.amount >= _wad, "DelayedWETH: insufficient unlocked withdrawal");
        require(wd.timestamp > 0, "DelayedWETH: withdrawal not unlocked");
        require(wd.timestamp + DELAY_SECONDS <= block.timestamp, "DelayedWETH: withdrawal delay not met");
        wd.amount -= _wad;
>       super.withdraw(_wad);
    }

WETH98.sol
    /// @inheritdoc IWETH
    function withdraw(uint256 wad) public virtual {
        require(balanceOf[msg.sender] >= wad);
        balanceOf[msg.sender] -= wad;
        payable(msg.sender).transfer(wad);
        emit Withdrawal(msg.sender, wad);
    }
```

If there is any discrepancy in the resolution of the game, the owner of DelayedWETH can call `recover()` to withdraw funds from the DelayedWETH contract, and at his own time, distribute the proper amount to the players.

```solidity
 /// @inheritdoc IDelayedWETH
    function recover(uint256 _wad) external {
        require(msg.sender == owner(), "DelayedWETH: not owner");
        uint256 amount = _wad < address(this).balance ? _wad : address(this).balance;
        payable(msg.sender).transfer(amount);
    }
```

The problem is that `recover()` doesn't invoke the `withdraw()` function, so `balanceOf[msg.sender]` doesn't decrease.

Let's say there is a problem in the resolution of the game.

Alice the Challenger is supposed to lose 1 ETH but she was wrongly accredited 1 ETH in `_distributeBond()` Her `credit[Alice] += 1 ETH`.

 The owner has the duration of `unlockTime` to withdraw all affected funds from DelayedWETH. Even if the owner withdraws all affected funds (say 2 ETH) in the DelayedWETH contract, there are many instances of DisputeGame going on, and the DelayedWETH contract is bound to have some funds.

After unlock time, Alice can still call `claimCredit()` to get 1 ETH because `balanceOf[msg.sender]` is not reduced when the owner recovers all the funds from DelayedWETH.sol.

Instead of losing 1 ETH, Alice gains 1 ETH instead. She effectively stole an ETH from another DisputeGame that is happening.

## Impact

In the case of a wrong resolution, players can still withdraw their bond at the expense of other player's bond.

## Code Snippet

https://github.com/sherlock-audit/2024-02-optimism-2024/blob/main/optimism/packages/contracts-bedrock/src/dispute/weth/DelayedWETH.sol#L57C1-L89C6

https://github.com/sherlock-audit/2024-02-optimism-2024/blob/main/optimism/packages/contracts-bedrock/src/dispute/weth/WETH98.sol#L40-L51

## Tool used

Manual Review

## Recommendation

When recovering funds, make sure the balance of the `msg.sender` which is the instance of the FaultDisputeGame, is deducted as well. Another recommendation is to initalize the guardian as the privileged user in every FaultDisputeGame instance, and let the guardian control the bond of the recipient, eg allowing the guardian to set `credit[_recipient] to zero.
