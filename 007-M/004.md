Real Olive Locust

medium

# The createdAt / resolvedAt timestamp is not validated again the respectedGameTypeUpdatedAt when checking if the message should be re-proved

## Summary

The createdAt / resolvedAt timestamp is not validated again the respectedGameTypeUpdatedAt when checking if the message should be re-proved

## Vulnerability Detail

```solidity
  /// @notice Sets the respected game type. Changing this value can alter the security properties of the system,
    ///         depending on the new game's behavior.
    /// @param _gameType The game type to consult for output proposals.
    function setRespectedGameType(GameType _gameType) external {
        require(msg.sender == guardian(), "OptimismPortal: only the guardian can set the respected game type");
        respectedGameType = _gameType;
        respectedGameTypeUpdatedAt = uint64(block.timestamp);
    }
```

In the new OptimismPortal2.sol,

the guardian has the privilege to change the respectedGameType,

when changing the game type, we update the state respectedGameTypeUpdatedAt

```solidity
respectedGameTypeUpdatedAt
```

then when check withdraw, the code enforce that the createdAt game dispute has to be greater than the respectedGameTypeUpdatedAt

```solidity
// The game type of the dispute game must be the respected game type. This was also checked in
// `proveWithdrawalTransaction`, but we check it again in case the respected game type has changed since
// the withdrawal was proven.
require(disputeGameProxy.gameType().raw() == respectedGameType.raw(), "OptimismPortal: invalid game type");

// The game must have been created after `respectedGameTypeUpdatedAt`. This is to prevent users from creating
// invalid disputes against a deployed game type while the off-chain challenge agents are not watching.
require(
    createdAt >= respectedGameTypeUpdatedAt,
    "OptimismPortal: dispute game created before respected game type was updated"
);
```

there are three game types:

```solidity
/// @title GameTypes
/// @notice A library that defines the IDs of games that can be played.
library GameTypes {
    /// @dev A dispute game type the uses the cannon vm.
    GameType internal constant CANNON = GameType.wrap(0);

    /// @dev A permissioned dispute game type the uses the cannon vm.
    GameType internal constant PERMISSIONED_CANNON = GameType.wrap(1);

    /// @notice A dispute game type that uses an alphabet vm.
    ///         Not intended for production use.
    GameType internal constant ALPHABET = GameType.wrap(255);
}
```

In case when the game type checked, the transaction hash allows reproving

```solidity
// We generally want to prevent users from proving the same withdrawal multiple times
// because each successive proof will update the timestamp. A malicious user can take
// advantage of this to prevent other users from finalizing their withdrawal. However,
// in the case that an honest user proves their withdrawal against a dispute game that
// resolves against the root claim, or the dispute game is blacklisted, we allow
// re-proving the withdrawal against a new proposal.
IDisputeGame oldGame = provenWithdrawal.disputeGameProxy;
require(
    provenWithdrawal.timestamp == 0 || oldGame.status() == GameStatus.CHALLENGER_WINS
        || disputeGameBlacklist[oldGame] || oldGame.gameType().raw() != respectedGameType.raw(),
    "OptimismPortal: withdrawal hash has already been proven, and the old dispute game is not invalid"
);
```

if provenWithdrawal.timestamp == 0 -> no withdrawal is proven -> allowed

if oldGame.status() == GameStatus.CHALLENGER_WINS -> challanger wins and defender lose -> allowed

if disputeGameBlacklist[oldGame] -> game is blocklisted -> allowed

if oldGame.gameType().raw() != respectedGameType.raw() -> respected game type changed -> allowed

but the reproving validating logic is missing an edge case when the reprove should be allowed

------------------------------------

consider the case:

1. user submit a proof, with the dispute game type CANNON, the game resolved as DEFENDER_WIN

2. second the gardian change the respected game type to PERMISSIONED_CANNON,

then the proofed message cannot be executed because the respected game type mismatch.

at this point user does not re-prove the message (for whatever reason)

3. third the guardian change the respected game type back to CANNON

at this point, the user's message cannot be reproven

because 

 provenWithdrawal.timestamp == 0  -> false

 oldGame.status() == GameStatus.CHALLENGER_WINS -> false

 disputeGameBlacklist[oldGame] -> false

 oldGame.gameType().raw() != respectedGameType.raw(), -> false

then the user's message can neither be executed nor be reproved because the reprove check does not check if the createdAt / resolvedAt is smaller than the respectedGameTypeUpdatedAt

## Impact

user's message can neither be executed nor be reproved because the reprove check does not validate against respectedGameTypeUpdatedAt

## Code Snippet

https://github.com/sherlock-audit/2024-02-optimism-2024/blob/f216b0d3ad08c1a0ead557ea74691aaefd5fd489/optimism/packages/contracts-bedrock/src/L1/OptimismPortal2.sol#L504

https://github.com/sherlock-audit/2024-02-optimism-2024/blob/f216b0d3ad08c1a0ead557ea74691aaefd5fd489/optimism/packages/contracts-bedrock/src/L1/OptimismPortal2.sol#L287

## Tool used

Manual Review

## Recommendation

allowing reproving message is the createdAt / resolvedAt timestamp is before the respectedGameTypeUpdatedAt if the respected game type is chanaged by guardian
