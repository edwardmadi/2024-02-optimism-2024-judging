Hot Stone Peacock

high

# Dispute games can be resolved with uncontested claims still unresolved

## Summary

Dispute games can be resolved with unresolved claims left behind, causing claimants to lose funds as they cannot claim their bonds.

## Vulnerability Detail

When resolving subgames with `FaultDisputeGame.resolveClaim()`, the function checks that the subgame does not contain an unresolved claim as such:

[FaultDisputeGame.sol#L445-L446](https://github.com/sherlock-audit/2024-02-optimism-2024/blob/f216b0d3ad08c1a0ead557ea74691aaefd5fd489/optimism/packages/contracts-bedrock/src/dispute/FaultDisputeGame.sol#L445-L446)

```solidity
            // INVARIANT: Cannot resolve a subgame containing an unresolved claim
            if (subgames[challengeIndex].length != 0) revert OutOfOrderResolution();
```

Two kinds of claims will pass this check:
1. Claims that have been resolved as their [`subgames` mapping would have been cleared](https://github.com/sherlock-audit/2024-02-optimism-2024/blob/f216b0d3ad08c1a0ead557ea74691aaefd5fd489/optimism/packages/contracts-bedrock/src/dispute/FaultDisputeGame.sol#L470-L471).
2. Uncontested claims. Their `subgames` mapping is already empty as they do not have any child claims. 

However, since claims can be resolved while their uncontested child claim is still unresolved, this allows the entire game to be resolved while there are still unresolved claims. For example:
- Assume the current game tree is `1 -> 2`.
- Node 2 is an uncontested claim as it does not have any child nodes.
- Call `resolveClaim()` on node 1, which works as node 2 is an uncontested claim.
- Call `resolve()` to resolve the entire dispute game.

Now, the entire dispute game has been resolved even though node 2 is still unresolved. This makes it impossible for node 2 to ever be resolved since `resolveClaim()` can only be called while the dispute game is in progress:

[FaultDisputeGame.sol#L405-L407](https://github.com/sherlock-audit/2024-02-optimism-2024/blob/f216b0d3ad08c1a0ead557ea74691aaefd5fd489/optimism/packages/contracts-bedrock/src/dispute/FaultDisputeGame.sol#L405-L407)

```solidity
    function resolveClaim(uint256 _claimIndex) external payable {
        // INVARIANT: Resolution cannot occur unless the game is currently in progress.
        if (status != GameStatus.IN_PROGRESS) revert GameNotInProgress();
```

The following PoC demonstrates the example above:

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.15;

import "src/libraries/DisputeTypes.sol";
import "src/libraries/DisputeErrors.sol";
import { FaultDisputeGame } from "src/dispute/FaultDisputeGame.sol";
import { FaultDisputeGame_Init } from "test/dispute/FaultDisputeGame.t.sol";

contract FaultDisputeGame_Test is FaultDisputeGame_Init {
    function test_dispute_game_can_resolve_with_unresolved_claims() public {
        // Create FaultDisputeGame
        Claim dummyClaim = Claim.wrap(bytes32(0));
        super.init(dummyClaim, dummyClaim, 0x10);

        // Create node 2 at challengeIndex 1
        uint256 bondAmount = gameProxy.getRequiredBond(Position.wrap(2));
        gameProxy.attack{value: bondAmount}(0, dummyClaim);

        // Warp to past GAME_DURATION. The game should be fully resolvable at this point
        skip(7 days);

        // Resolve node 1 and the entire dispute game
        gameProxy.resolveClaim(0);
        gameProxy.resolve();

        // Node 2 can no longer be resolved
        vm.expectRevert(GameNotInProgress.selector);
        gameProxy.resolveClaim(1);
    }
}
```

## Impact

Dispute games can be resolved with uncontested claims that are still unresolved. These uncontested claims can never be resolved, therefore, the claimants of these claims will permanently lose their bonds.

Note that in this finding, the dispute game does resolve correctly. The impact here is that a correct dispute game resolution can occur with a loss of funds to claimants, which is not considered out-of-scope by this statement:

> Participants should assume that the FaultDisputeGame can resolve incorrectly (i.e.g, can resolve to DEFENDER_WINS when it should resolve to CHALLENGER_WINS or vice versa). Reports that demonstrate an incorrect resolution of the FaultDisputeGame are appreciated but will not be considered valid rewardable findings for this specific contest.

## Code Snippet

https://github.com/sherlock-audit/2024-02-optimism-2024/blob/f216b0d3ad08c1a0ead557ea74691aaefd5fd489/optimism/packages/contracts-bedrock/src/dispute/FaultDisputeGame.sol#L445-L446

https://github.com/sherlock-audit/2024-02-optimism-2024/blob/f216b0d3ad08c1a0ead557ea74691aaefd5fd489/optimism/packages/contracts-bedrock/src/dispute/FaultDisputeGame.sol#L405-L407

## Tool used

Manual Review

## Recommendation

Modify the check in `resolveClaim()` to only allow resolving a claim after all its child claims have been resolved:

```diff
            // INVARIANT: Cannot resolve a subgame containing an unresolved claim
-           if (subgames[challengeIndex].length != 0) revert OutOfOrderResolution();
+           if (claimData[challengeIndex].bond != CLAIMED_BOND_FLAG) revert OutOfOrderResolution();
```

This means that uncontested claims should not be implicitly treated as resolved; they have to be resolved with `resolveClaim()` to be considered resolved.
