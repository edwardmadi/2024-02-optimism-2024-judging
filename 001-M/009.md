Flat Fleece Snail

medium

# `respectedGameType` is uninitialized in `OptimismPortal2` proxy

## Summary

`respectedGameType` is uninitialized in `OptimismPortal2` proxy

## Vulnerability Detail

The `respectedGameType` in the `OptimismPortal2` is neither immutable nor constant

[OptimismPortal2.sol#L87-L88](https://github.com/sherlock-audit/2024-02-optimism-2024/blob/main/optimism/packages/contracts-bedrock/src/L1/OptimismPortal2.sol#L87-L88)
```solidity
    /// @notice The game type that the OptimismPortal consults for output proposals.
    GameType public respectedGameType;
```

Furthermore, it is not initialized in `initialize`

[OptimismPortal2.sol#L147-L162](https://github.com/sherlock-audit/2024-02-optimism-2024/blob/main/optimism/packages/contracts-bedrock/src/L1/OptimismPortal2.sol#L147-L162)
```solidity
    function initialize(
        DisputeGameFactory _disputeGameFactory,
        SystemConfig _systemConfig,
        SuperchainConfig _superchainConfig
    )
        public
        initializer
    {
        disputeGameFactory = _disputeGameFactory;
        systemConfig = _systemConfig;
        superchainConfig = _superchainConfig;
        if (l2Sender == address(0)) {
            l2Sender = Constants.DEFAULT_L2_SENDER;
        }
        __ResourceMetering_init();
    }
```

Instead it is initialized in the constructor of the implementation, which has no effect.

[OptimismPortal2.sol#L126-L141](https://github.com/sherlock-audit/2024-02-optimism-2024/blob/main/optimism/packages/contracts-bedrock/src/L1/OptimismPortal2.sol#L126-L141)
```solidity
    /// @notice Constructs the OptimismPortal contract.
    constructor(
        uint256 _proofMaturityDelaySeconds,
        uint256 _disputeGameFinalityDelaySeconds,
        GameType _initialRespectedGameType
    ) {
        PROOF_MATURITY_DELAY_SECONDS = _proofMaturityDelaySeconds;
        DISPUTE_GAME_FINALITY_DELAY_SECONDS = _disputeGameFinalityDelaySeconds;
        respectedGameType = _initialRespectedGameType;

        initialize({
            _disputeGameFactory: DisputeGameFactory(address(0)),
            _systemConfig: SystemConfig(address(0)),
            _superchainConfig: SuperchainConfig(address(0))
        });
    }
```

This means that when the `OptimismPortal2` proxy is deployed and initialized, the `respectedGameType` remains uninitialized in the proxy because it is neither immutable nor constant nor initialized in `initialize`

The effect is that any attempt to `proveWithdrawalTransaction` will revert initially, because of the failing `require` check below.

[OptimismPortal2.sol#L242-L325](https://github.com/sherlock-audit/2024-02-optimism-2024/blob/main/optimism/packages/contracts-bedrock/src/L1/OptimismPortal2.sol#L242-L325)
```solidity
    function proveWithdrawalTransaction(
        Types.WithdrawalTransaction memory _tx,
        uint256 _disputeGameIndex,
        Types.OutputRootProof calldata _outputRootProof,
        bytes[] calldata _withdrawalProof
    )
        external
        whenNotPaused
    {
        // Prevent users from creating a deposit transaction where this address is the message
        // sender on L2. Because this is checked here, we do not need to check again in
        // `finalizeWithdrawalTransaction`.
        require(_tx.target != address(this), "OptimismPortal: you cannot send messages to the portal contract");

        // Fetch the dispute game proxy from the `DisputeGameFactory` contract.
        (GameType gameType,, IDisputeGame gameProxy) = disputeGameFactory.gameAtIndex(_disputeGameIndex);
        Claim outputRoot = gameProxy.rootClaim();

        // The game type of the dispute game must be the respected game type.
=>      require(gameType.raw() == respectedGameType.raw(), "OptimismPortal: invalid game type");

        // Verify that the output root can be generated with the elements in the proof.
        require(
            outputRoot.raw() == Hashing.hashOutputRootProof(_outputRootProof),
            "OptimismPortal: invalid output root proof"
        );
        ...
```

Although the guardian can set the respected game type later on, this will break core contract functionality initially as users realize they cannot prove withdrawal transaction.

[OptimismPortal2.sol#L448-L452](https://github.com/sherlock-audit/2024-02-optimism-2024/blob/main/optimism/packages/contracts-bedrock/src/L1/OptimismPortal2.sol#L448-L452)
```solidity
    function setRespectedGameType(GameType _gameType) external {
        require(msg.sender == guardian(), "OptimismPortal: only the guardian can set the respected game type");
        respectedGameType = _gameType;
        respectedGameTypeUpdatedAt = uint64(block.timestamp);
    }
```

## Impact

This will break core contract functionality initially as users realize they cannot prove withdrawal transaction.

## Code Snippet

https://github.com/sherlock-audit/2024-02-optimism-2024/blob/main/optimism/packages/contracts-bedrock/src/L1/OptimismPortal2.sol#L126-L141

## Tool used

Manual Review

## Recommendation

`respectedGameType` should be initialized in `initialize` rather than the constructor.
