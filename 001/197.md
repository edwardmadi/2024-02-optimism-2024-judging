Tart Ultraviolet Kitten

high

# The in-game chess clock runs incorrectly, counting the seconds spend in favor of the non-answering team and leading to incorrect resolutions.

## Summary

The in-game chess clock runs incorrectly, counting the seconds spend in favor of the non-answering team and leading to incorrect resolutions.

## Vulnerability Detail

The game runs a chess clock for each time, at every moment any unanswered claim counts against the pending response team, until it's timer runs out and it can no longer perform moves. To resolve a claim, the code logic needs to make sure that it can no longer be answered by a counter-move, i.e. the opponent's timer has ran to zero. This is what the code below at `resolveClaim()` aims to do:

```solidity
ClaimData storage parent = claimData[_claimIndex];
// INVARIANT: Cannot resolve a subgame unless the clock of its root has expired
uint64 parentClockDuration = parent.clock.duration().raw();
uint64 timeSinceParentMove = uint64(block.timestamp) - parent.clock.timestamp().raw();
if (parentClockDuration + timeSinceParentMove <= GAME_DURATION.raw() >> 1) {
    revert ClockNotExpired();
}
```

There is a key error - it adds the duration of the target `claimIndex` to the time elapsed, instead of its parent's duration. Essentially we are running the opponents clock on our turn. 

We can see below how the `move()` function uses the *grandparent's clock* correctly:
```solidity
Clock grandparentClock;
if (parent.parentIndex != type(uint32).max) {
    grandparentClock = claimData[parent.parentIndex].clock;
}
// Compute the duration of the next clock. This is done by adding the duration of the
// grandparent claim to the difference between the current block timestamp and the
// parent's clock timestamp.
Duration nextDuration = Duration.wrap(
    uint64(
        // First, fetch the duration of the grandparent claim.
        grandparentClock.duration().raw()
        // Second, add the difference between the current block timestamp and the
        // parent's clock timestamp.
        + block.timestamp - parent.clock.timestamp().raw()
    )
);
// INVARIANT: A move can never be made once its clock has exceeded `GAME_DURATION / 2`
//            seconds of time.
if (nextDuration.raw() > GAME_DURATION.raw() >> 1) revert ClockTimeExceeded();
```

The same logic should be applied in `resolveClaim()`.

The issue is easily exploitable to win an unfair game:
Assume `GAME_DURATION = 100`

- Dishonest team proposes a malicious root at T=0
- Honest team responds with valid claim at T=5 , duration = 5
- Dishonest team waits until T=55 and makes invalid claim, duration = 50
- In proper functioning of contract, honest team should be able to respond at T=60, duration = 10 (they spend 5 seconds per response)
- However, due to the bug, dishonest team can resolve at T = 56. It counted the 50 seconds dishonest team spent as 50 seconds of honest team. 

The safety check below resolves to `false`, not reverting the bad resolution:
```solidity
uint64 timeSinceParentMove = uint64(block.timestamp) - parent.clock.timestamp().raw();
if (parentClockDuration + timeSinceParentMove <= GAME_DURATION.raw() >> 1) {
    revert ClockNotExpired();
}
```


## Impact

Using the wrong clock is fatal:
- Honest team can lose the game and lose their bonds
- A malicious root will be proposed successfully, compromising all funds locked on the L1.

## Code Snippet

```solidity
ClaimData storage parent = claimData[_claimIndex];
// INVARIANT: Cannot resolve a subgame unless the clock of its root has expired
uint64 parentClockDuration = parent.clock.duration().raw();
uint64 timeSinceParentMove = uint64(block.timestamp) - parent.clock.timestamp().raw();
if (parentClockDuration + timeSinceParentMove <= GAME_DURATION.raw() >> 1) {
    revert ClockNotExpired();
}
```

## Tool used

Manual Review

## Recommendation

Use the parent's clock when calculating duration in the `resolveClaim()` function.
