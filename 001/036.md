Real Olive Locust

high

# Game duration validation for the move that create later is too short

## Summary

Game duration validation for the move that create later is too short

## Vulnerability Detail

When a dispute game is initialized, the clock is set:

```solidity
    // Set the root claim
    claimData.push(
        ClaimData({
            parentIndex: type(uint32).max,
            counteredBy: address(0),
            claimant: tx.origin,
            bond: uint128(msg.value),
            claim: rootClaim(),
            position: ROOT_POSITION,
            clock: LibClock.wrap(Duration.wrap(0), Timestamp.wrap(uint64(block.timestamp)))
        })
    );
```

we track the clock by duration and the block timestamp,

when resolving a claim,

this is how we check and validate the duration

```solidity
function resolveClaim(uint256 _claimIndex) external payable {
    // INVARIANT: Resolution cannot occur unless the game is currently in progress.
    if (status != GameStatus.IN_PROGRESS) revert GameNotInProgress();

    ClaimData storage parent = claimData[_claimIndex];

    // INVARIANT: Cannot resolve a subgame unless the clock of its root has expired
    uint64 parentClockDuration = parent.clock.duration().raw();

    uint64 timeSinceParentMove = uint64(block.timestamp) - parent.clock.timestamp().raw();
    if (parentClockDuration + timeSinceParentMove <= GAME_DURATION.raw() >> 1) {
        revert ClockNotExpired();
    }
```

GAME_DURATION is 7 days,

half of 7day is 3.5 days.

suppose we tries to resolve the claim at index 0 at block timestamp 1000

parent =  claimData[_claimIndex],

which is 

```solidity
ClaimData({
    parentIndex: type(uint32).max,
    counteredBy: address(0),
    claimant: tx.origin,
    bond: uint128(msg.value),
    claim: rootClaim(),
    position: ROOT_POSITION,
    clock: LibClock.wrap(Duration.wrap(0), Timestamp.wrap(uint64(block.timestamp)))
})
```

then parentClockDuration = parent.clock.duration().raw() = 0

timeSinceParentMove = block.timestamp -  parent.clock.timestamp().raw() = time elapses between the first claim data created and the current time,

if this time exceed 3.5 days, the claim can be resolved,

now one day passes, and the block timestamp is fast foward to 1000 + 86400

However, assume user make a move for index 0 (0 is the challangeIndex) at timestamp 1000 + 86400

```solidity
  // Get the parent. If it does not exist, the call will revert with OOB.
        ClaimData memory parent = claimData[_challengeIndex];
```

```solidity
Clock grandparentClock;
if (parent.parentIndex != type(uint32).max) {
    grandparentClock = claimData[parent.parentIndex].clock;
}
```

grandparentClock will stay empty because parent.parentIndex is type(uint32).max

then next duration is:

```solidity
 Duration nextDuration = Duration.wrap(
            uint64(
                // First, fetch the duration of the grandparent claim.
                grandparentClock.duration().raw()
                // Second, add the difference between the current block timestamp and the
                // parent's clock timestamp.
                + block.timestamp - parent.clock.timestamp().raw()
            )
        );
```

grandparentClock.duration().raw() is 0

block.timestamp - parent.clock.timestamp().raw() is the time elapses between the first claim data created and the current time

suppose 1 days has past

```solidity
// Construct the next clock with the new duration and the current block timestamp.
Clock nextClock = LibClock.wrap(nextDuration, Timestamp.wrap(uint64(block.timestamp)));
```

the next clock duration will be 1 days, with the current timestamp

Now suppose user wants to resolve the claim at index 0

```solidity
function resolveClaim(uint256 _claimIndex) external payable {
    // INVARIANT: Resolution cannot occur unless the game is currently in progress.
    if (status != GameStatus.IN_PROGRESS) revert GameNotInProgress();

    ClaimData storage parent = claimData[_claimIndex];

    // INVARIANT: Cannot resolve a subgame unless the clock of its root has expired
    uint64 parentClockDuration = parent.clock.duration().raw();

    uint64 timeSinceParentMove = uint64(block.timestamp) - parent.clock.timestamp().raw();
    if (parentClockDuration + timeSinceParentMove <= GAME_DURATION.raw() >> 1) {
        revert ClockNotExpired();
    }
```

again, 

parentClockDuration + timeSinceParentMove  = 1 days 

Now suppose user wants to resolve the claim at index 1 (assume that user wants to resolve the claim within the same block)

parentClockDuration will be 1 days, timeSinceParentMove is 0,

parentClockDuration + timeSinceParentMove = 1 days,

but wait, we just create a move and the there should be 3.5 days time windows,

claim at index 0 needs to wait for 3.5 days to pass the game duration check

but the claim at index 1 only only needs to wait for 2.5 days to pass the game duration check when resolving the claim.

this means that the claim that created later has less time left for counters to fight back, this would greatly reduce the cost for user that make a invalid claim later,

because even when a invalid claim is raised, the attacker has much less time to make a valid move.


## Impact

.

## Code Snippet

https://github.com/sherlock-audit/2024-02-optimism-2024/blob/f216b0d3ad08c1a0ead557ea74691aaefd5fd489/optimism/packages/contracts-bedrock/src/dispute/FaultDisputeGame.sol#L272

https://github.com/sherlock-audit/2024-02-optimism-2024/blob/f216b0d3ad08c1a0ead557ea74691aaefd5fd489/optimism/packages/contracts-bedrock/src/dispute/FaultDisputeGame.sol#L563

below is a POC:

```solidity
// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.13;

import "forge-std/Test.sol";
import "forge-std/console.sol";

/// @notice A dedicated timestamp type.
type Timestamp is uint64;

/// @notice A dedicated duration type.
/// @dev Unit: seconds
type Duration is uint64;

type Clock is uint128;

/// @title LibClock
/// @notice This library contains helper functions for working with the `Clock` type.
library LibClock {
    /// @notice Packs a `Duration` and `Timestamp` into a `Clock` type.
    /// @param _duration The `Duration` to pack into the `Clock` type.
    /// @param _timestamp The `Timestamp` to pack into the `Clock` type.
    /// @return clock_ The `Clock` containing the `_duration` and `_timestamp`.
    function wrap(Duration _duration, Timestamp _timestamp) internal pure returns (Clock clock_) {
        assembly {
            clock_ := or(shl(0x40, _duration), _timestamp)
        }
    }

    /// @notice Pull the `Duration` out of a `Clock` type.
    /// @param _clock The `Clock` type to pull the `Duration` out of.
    /// @return duration_ The `Duration` pulled out of `_clock`.
    function duration(Clock _clock) internal pure returns (Duration duration_) {
        // Shift the high-order 64 bits into the low-order 64 bits, leaving only the `duration`.
        assembly {
            duration_ := shr(0x40, _clock)
        }
    }

    /// @notice Pull the `Timestamp` out of a `Clock` type.
    /// @param _clock The `Clock` type to pull the `Timestamp` out of.
    /// @return timestamp_ The `Timestamp` pulled out of `_clock`.
    function timestamp(Clock _clock) internal pure returns (Timestamp timestamp_) {
        // Clean the high-order 192 bits by shifting the clock left and then right again, leaving
        // only the `timestamp`.
        assembly {
            timestamp_ := shr(0xC0, shl(0xC0, _clock))
        }
    }

    /// @notice Get the value of a `Clock` type in the form of the underlying uint128.
    /// @param _clock The `Clock` type to get the value of.
    /// @return clock_ The value of the `Clock` type as a uint128 type.
    function raw(Clock _clock) internal pure returns (uint128 clock_) {
        assembly {
            clock_ := _clock
        }
    }
}


/// @title LibDuration
/// @notice This library contains helper functions for working with the `Duration` type.
library LibDuration {
    /// @notice Get the value of a `Duration` type in the form of the underlying uint64.
    /// @param _duration The `Duration` type to get the value of.
    /// @return duration_ The value of the `Duration` type as a uint64 type.
    function raw(Duration _duration) internal pure returns (uint64 duration_) {
        assembly {
            duration_ := _duration
        }
    }
}

/// @title LibTimestamp
/// @notice This library contains helper functions for working with the `Timestamp` type.
library LibTimestamp {
    /// @notice Get the value of a `Timestamp` type in the form of the underlying uint64.
    /// @param _timestamp The `Timestamp` type to get the value of.
    /// @return timestamp_ The value of the `Timestamp` type as a uint64 type.
    function raw(Timestamp _timestamp) internal pure returns (uint64 timestamp_) {
        assembly {
            timestamp_ := _timestamp
        }
    }
}

using LibDuration for Duration global;
using LibClock for Clock global;
using LibTimestamp for Timestamp global;


contract CounterTest is Test {

    using stdStorage for StdStorage;
    StdStorage stdlib;


    function setUp() public {

    }

    function validateGameDuration(Clock parent) public view returns (uint64){

          // INVARIANT: Cannot resolve a subgame unless the clock of its root has expired
        uint64 parentClockDuration = parent.duration().raw();

        uint64 timeSinceParentMove = uint64(block.timestamp) - parent.timestamp().raw();

        return 3.5 days - (timeSinceParentMove + parentClockDuration);

    }   

    function testTimestamp() public {


        uint64 t1 = uint64(block.timestamp);

        console.log("t1: ", t1);

        // this is how the clock is set when the dispute game is initialized
        // https://github.com/sherlock-audit/2024-02-optimism-2024/blob/f216b0d3ad08c1a0ead557ea74691aaefd5fd489/optimism/packages/contracts-bedrock/src/dispute/FaultDisputeGame.sol#L563
        Clock clock = LibClock.wrap(Duration.wrap(0), Timestamp.wrap(uint64(t1)));

        // wait time check we need to wait for 3.5 days
        
        uint256 wait_time = validateGameDuration(clock);

        console.log("wait_time: ", wait_time);

        vm.warp(1 days);

        // make a move after 1 day

        // this is how the next clock is set when a move is make against index 0
        // https://github.com/sherlock-audit/2024-02-optimism-2024/blob/f216b0d3ad08c1a0ead557ea74691aaefd5fd489/optimism/packages/contracts-bedrock/src/dispute/FaultDisputeGame.sol#L272
        Clock grandparentClock;

        Duration nextDuration = Duration.wrap(
            uint64(
                // First, fetch the duration of the grandparent claim.
                grandparentClock.duration().raw()
                // Second, add the difference between the current block timestamp and the
                // parent's clock timestamp.
                + block.timestamp - clock.timestamp().raw()
            )
        );

        Clock nextClock = LibClock.wrap(nextDuration, Timestamp.wrap(uint64(block.timestamp)));

        uint256 wait_time_2 = validateGameDuration(nextClock);

        console.log("wait_time_2: ", wait_time_2);


    }


}
```

if we run the POC:

```solidity
forge test -vv --match-test "testTimestamp"
```

the out pus is:

```solidity
 t1:  1
  wait_time:  302400
  wait_time_2:  216001
```

showing that to resolve the claim at index 0, the wait time is 3.5 days, but to resolve the claim at index 1, the wait time is much shorter even the claim at index 1 is just created.

## Tool used

Manual Review

## Recommendation

N/A