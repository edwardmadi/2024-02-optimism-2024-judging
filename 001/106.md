Damaged Chartreuse Skunk

high

# `resolveClaim` will not allow solving subclaims once game is `resolve`d, causing bonds to be lost

## Summary
Out of order `resolveClaim` can be performed, causing loss of bonds,

## Vulnerability Detail

The check to `resolve` is as follows:
https://github.com/sherlock-audit/2024-02-optimism-2024/blob/main/optimism/packages/contracts-bedrock/src/dispute/FaultDisputeGame.sol#L386-L392

`resolveClaim` is instead written in this way:

https://github.com/sherlock-audit/2024-02-optimism-2024/blob/main/optimism/packages/contracts-bedrock/src/dispute/FaultDisputeGame.sol#L405-L424


While it is possible for `subgameAtRootResolved` to be `true` while `status != GameStatus.IN_PROGRESS`

Anyone is going to be able to `resolve` the game, preventing benign users from resolving their subGames

It's worth noting that any benign user has no reason to wait for others to settle, they would most likely do this not even out of a malicious motive, but simply to be able to prove their withdrawal

## Impact

Bonds from subclaims that are not `settled` before `resolve` will be lost permanently

## Code Snippet

The POC will pass and the bond will never be claimable

We demonstrate both flows and show that the game does resolve in the correct state

Showing that bond claiming has a race condition that causes the bond unclaimable and lost

We demonstrate this by `resolve`ing a game and we show that if we claim out of order `proxy.resolveClaim(0)` first, then we can `proxy.resolve()` and we can never call `proxy.resolveClaim(1)`

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.15;

import {Test} from "forge-std/Test.sol";
import {console2} from "forge-std/console2.sol";

import "src/dispute/DisputeGameFactory.sol";
import "src/dispute/FaultDisputeGame.sol";
import "src/dispute/AnchorStateRegistry.sol";
import "src/dispute/lib/LibPosition.sol";

contract FaultDisputesTest is Test {

    fallback () external payable {

    }
    function testOutOfOrderClaimsLoseBonds() public {
        DisputeGameFactory factory = new DisputeGameFactory();

        AnchorStateRegistry registry = new AnchorStateRegistry(IDisputeGameFactory(factory));

        FaultDisputeGame firstGame = new FaultDisputeGame(
            GameType.wrap(1),
            Claim.wrap(bytes32(0x0)),
            73, // MAX_DEPTH
            30, // 30 SPLIT_DEPTH
            Duration.wrap(604800), // 7 days

            IBigStepper(address(this)),
            IDelayedWETH(address(this)),
            IAnchorStateRegistry(registry),
            123123
        );

        factory.setImplementation(GameType.wrap(1), IDisputeGame(firstGame));

        AnchorStateRegistry.StartingAnchorRoot[] memory initialRoots = new AnchorStateRegistry.StartingAnchorRoot[](1);
        initialRoots[0].gameType = GameType.wrap(1);
        initialRoots[0].outputRoot = OutputRoot({
            root: Hash.wrap(bytes32(uint256(123123))),
            l2BlockNumber: 1
        });

        registry.initialize(
            initialRoots
        );

        FaultDisputeGame proxy = FaultDisputeGame(payable(address(factory.create(
            GameType.wrap(1),
            Claim.wrap(bytes32(uint256(1231231231))),
            abi.encode(2)
        ))));

        uint256 amt = proxy.getRequiredBond(LibPosition.wrap(uint64(1), 0));
        proxy.move{value: amt}(0, Claim.wrap(bytes32(uint256(1))), true);

        vm.warp(block.timestamp + proxy.gameDuration().raw() + 1);

        uint256 happySnapshot = vm.snapshot();
        // NOTE: Resolves correctly as `CHALLENGER_WINS` but challenger loses their bond
        
        // Resolves as intended
        proxy.resolveClaim(1);
        proxy.resolveClaim(0);
        proxy.resolve();
        console2.log("Proper order claim", uint256(proxy.status()));

        // Resolves while skipping
        vm.revertTo(happySnapshot);

        proxy.resolveClaim(0);
        proxy.resolve();
        console2.log("Skipped claim", uint256(proxy.status()));

        // Lost bond
        vm.expectRevert();
        proxy.resolveClaim(1);
    }
}

```

To allow the code to compile more rapidly I edited the source code in this way:

### Changed `DisputeGameFactory`

```solidity
    constructor() OwnableUpgradeable() {
        initialize(address(msg.sender)); // NOTE: Changed on purpose
    }
```

### Change `AnchorStateRegistry`

```solidity
    constructor(IDisputeGameFactory _disputeGameFactory) {
        DISPUTE_GAME_FACTORY = _disputeGameFactory;

        // Initialize the implementation with an empty array of starting anchor roots.
        // initialize(new StartingAnchorRoot[](0)); // NOTE: So we can initialize
    }
```

## Tool used

Manual Review + Foundry

## Recommendation

Refactor the logic to maintain the invariants that:
- Ensure that no game is resolved multiple times
- Games that are resolved can be settled after the `subgameAtRootResolved` as to prevent race conditions

