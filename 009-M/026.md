Real Olive Locust

medium

# When the dispute game resolves incorrect / when dispute game is blocklisted, all other dispute game still consume the incorrect data from AnchorStateRegistry

## Summary

When the dispute game resolves incorrect / when dispute game is blocklisted, all other dispute game still consume the incorrect data from AnchorStateRegistry

## Vulnerability Detail

There is an assumption that the dispute game can be resolved incorrectly

when the status is expected to CHALLANGER_WIN, the status goes to DEFENER_WIN, this allows a invalid output root state is submit

In this case, the guardian should blocklist the dispute game

when the status is expected to DEFENER_WIN while the status goes to CHALLANGER_WIN, this is less a concern

but in the first case, when the status is expected to CHALLANGER_WIN, the status goes to DEFENER_WIN,

even the guardian blocklist the dispute game but not let incorrect withdraw transaction executed,

the incorrect result still write to the anchorStateRegistry

```solidity
function resolve() external returns (GameStatus status_) {
    // INVARIANT: Resolution cannot occur unless the game is currently in progress.
    if (status != GameStatus.IN_PROGRESS) revert GameNotInProgress();

    // INVARIANT: Resolution cannot occur unless the absolute root subgame has been resolved.
    if (!subgameAtRootResolved) revert OutOfOrderResolution();

    // Update the global game status; The dispute has concluded.
    status_ = claimData[0].counteredBy == address(0) ? GameStatus.DEFENDER_WINS : GameStatus.CHALLENGER_WINS;
    resolvedAt = Timestamp.wrap(uint64(block.timestamp));

    // Update the status and emit the resolved event, note that we're performing an assignment here.
    emit Resolved(status = status_);

    // Try to update the anchor state, this should not revert.
    ANCHOR_STATE_REGISTRY.tryUpdateAnchorState();
}
```

the only way to update the ANCHOR_STATE_REGISTRY state is by calling:

```solidity
    ANCHOR_STATE_REGISTRY.tryUpdateAnchorState();
```

then we call:

```solidity
   function tryUpdateAnchorState() external {
        // Grab the game and game data.
        IFaultDisputeGame game = IFaultDisputeGame(msg.sender);
        (GameType gameType, Claim rootClaim, bytes memory extraData) = game.gameData();

        // Grab the verified address of the game based on the game data.
        // slither-disable-next-line unused-return
        (IDisputeGame factoryRegisteredGame,) =
            DISPUTE_GAME_FACTORY.games({ _gameType: gameType, _rootClaim: rootClaim, _extraData: extraData });

        // Must be a valid game.
        require(
            address(factoryRegisteredGame) == address(game),
            "AnchorStateRegistry: fault dispute game not registered with factory"
        );

        // No need to update anything if the anchor state is already newer.
        if (game.l2BlockNumber() <= anchors[gameType].l2BlockNumber) {
            return;
        }

        // Must be a game that resolved in favor of the state.
        if (game.status() != GameStatus.DEFENDER_WINS) {
            return;
        }

        // Actually update the anchor state.
        anchors[gameType] = OutputRoot({ l2BlockNumber: game.l2BlockNumber(), root: Hash.wrap(game.rootClaim().raw()) });
    }
```


as we can see, if the DEFENER_WINS, the output root data (block number + the root claim) is written to the anchors[gameType]

```solidity
      // Actually update the anchor state.
        anchors[gameType] = OutputRoot({ l2BlockNumber: game.l2BlockNumber(), root: Hash.wrap(game.rootClaim().raw()) });
    }
```

then going onward, all the game type consider the anchors[gameType] as a source of truth to derive the data state when initliaze a new dispute game

```solidity
  function initialize() public payable virtual {
        // SAFETY: Any revert in this function will bubble up to the DisputeGameFactory and
        // prevent the game from being created.
        //
        // Implicit assumptions:
        // - The `gameStatus` state variable defaults to 0, which is `GameStatus.IN_PROGRESS`
        // - The dispute game factory will enforce the required bond to initialize the game.
        //
        // Explicit checks:
        // - The game must not have already been initialized.
        // - An output root cannot be proposed at or before the starting block number.

        // INVARIANT: The game must not have already been initialized.
        if (initialized) revert AlreadyInitialized();

        // Grab the latest anchor root.
        (Hash root, uint256 rootBlockNumber) = ANCHOR_STATE_REGISTRY.anchors(GAME_TYPE);

        // Should only happen if this is a new game type that hasn't been set up yet.
        if (root.raw() == bytes32(0)) revert AnchorRootNotFound();

        // Set the starting output root.
        startingOutputRoot = OutputRoot({ l2BlockNumber: rootBlockNumber, root: root });
```

## Impact

When the dispute game resolves incorrect / when dispute game is blocklisted, all other dispute game still consume the incorrect data from AnchorStateRegistry

result in incorrect dispute state

## Code Snippet

https://github.com/sherlock-audit/2024-02-optimism-2024/blob/f216b0d3ad08c1a0ead557ea74691aaefd5fd489/optimism/packages/contracts-bedrock/src/dispute/AnchorStateRegistry.sol#L81

https://github.com/sherlock-audit/2024-02-optimism-2024/blob/f216b0d3ad08c1a0ead557ea74691aaefd5fd489/optimism/packages/contracts-bedrock/src/dispute/FaultDisputeGame.sol#L529

## Tool used

Manual Review

## Recommendation

In the AnchorStateRegistry.sol,

add a validation, if the dispute game is blocklisted, just return and do not update the anchors[gameType] output root data:

```solidity
 function tryUpdateAnchorState() external {
        // Grab the game and game data.
        IFaultDisputeGame game = IFaultDisputeGame(msg.sender);
        (GameType gameType, Claim rootClaim, bytes memory extraData) = game.gameData();

        // Grab the verified address of the game based on the game data.
        // slither-disable-next-line unused-return
        (IDisputeGame factoryRegisteredGame,) =
            DISPUTE_GAME_FACTORY.games({ _gameType: gameType, _rootClaim: rootClaim, _extraData: extraData });

        // Must be a valid game.
        require(
            address(factoryRegisteredGame) == address(game),
            "AnchorStateRegistry: fault dispute game not registered with factory"
        );

        // No need to update anything if the anchor state is already newer.
        if (game.l2BlockNumber() <= anchors[gameType].l2BlockNumber) {
            return;
        }

        // Must be a game that resolved in favor of the state.
        if (game.status() != GameStatus.DEFENDER_WINS) {
            return;
        }

        // add check here to prevent blocklist / incorrectly resolved dispute game write the data
        if (OptimismPoral2(portal).disputeGameBlacklist(game) is True) {
            return
        }

        // Actually update the anchor state.
        anchors[gameType] = OutputRoot({ l2BlockNumber: game.l2BlockNumber(), root: Hash.wrap(game.rootClaim().raw()) });
    }
```

