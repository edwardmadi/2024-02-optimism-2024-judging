Bitter Ivory Baboon

medium

# OptimismPortal2 cannot be initialized

## Summary

`OptimismPortal2` is a new implementation of an existing proxy contract, which is already initialized. Therefore, repeated initialization will revert.

## Vulnerability Detail

The `OptimismPortal2::initialize` function utilizes the `initializer` modifier from OpenZeppelin (OZ). This modifier defines a protected initializer function that can be invoked at most once by checking that the private `_initialized` property is less than 1.

```solidity
modifier initializer() {
    ...
    require(
        (isTopLevelCall && _initialized < 1) || (!Address.isContract(address(this)) && _initialized == 1),
       "Initializable: contract is already initialized"
    );
    ...
}
```

`OptimismPortal2` is a new implementation of the existing version of the contract `OptimismPortal` (an upgradeable [proxy](https://etherscan.io/address/0xbEb5Fc579115071764c7423A4f12eDde41f106Ed)), which also has an `initialize` function and has already been initialized. This means that the `_initialized` property is equal to 1, and the initialization of the new implementation will revert.

## Impact

The `OptimismPortal2` contract cannot be deployed and initialized. If an update is attempted in two steps - first updating the implementation of the proxy and then initializing - the contract will remain in an uninitialized state.

## POC

```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.15;

import { Test } from "forge-std/Test.sol";
import { Proxy } from "../src/universal/Proxy.sol";
import { ProxyAdmin } from "../src/universal/ProxyAdmin.sol";

import { OptimismPortal2 } from "../src/L1/OptimismPortal2.sol";
import "../src/libraries/DisputeTypes.sol";

import { DisputeGameFactory } from "../src/dispute/DisputeGameFactory.sol";
import { SystemConfig } from "../src/L1/SystemConfig.sol";
import { SuperchainConfig } from "../src/L1/SuperchainConfig.sol";

contract POC is Test {

    address optimismPortalProxy = 0xbEb5Fc579115071764c7423A4f12eDde41f106Ed;

    function testInitializeReverts() external {
        vm.prank(address(0));
        address admin = Proxy(payable(optimismPortalProxy)).admin();

        vm.startPrank(admin);
        OptimismPortal2 optimismPortal2 = new OptimismPortal2(0, 0, GameType.wrap(0));
        Proxy(payable(optimismPortalProxy)).upgradeTo(address(optimismPortal2));

        vm.expectRevert("Initializable: contract is already initialized");
        OptimismPortal2(payable(optimismPortalProxy)).initialize(
            DisputeGameFactory(address(0)),
            SystemConfig(address(0)),
            SuperchainConfig(address(0))
        );
    }
}
```

## Code Snippet

https://github.com/sherlock-audit/2024-02-optimism-2024/blob/main/optimism/packages/contracts-bedrock/src/L1/OptimismPortal2.sol#L153

## Tool used

Manual Review

## Recommendation

```diff
@@ -150,7 +150,7 @@ contract OptimismPortal2 is Initializable, ResourceMetering, ISemver {
         SuperchainConfig _superchainConfig
     )
         public
-        initializer
+        reinitializer(2)
     {
         disputeGameFactory = _disputeGameFactory;
         systemConfig = _systemConfig;
```