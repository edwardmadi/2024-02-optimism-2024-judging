Keen Orchid Swan

medium

# Overflow in LibGameType library can lead to more games being respected in the portal contract

## Summary
The library function can overflow if a number higher than `255` is passed in the function. Because `GameType` is type `uint32` and the library `raw` function returns `uint8`, more input parameters (`uint32`) will result in the same number `gametype_`

```solidity
/// @title LibGameType
/// @notice This library contains helper functions for working with the `GameType` type.
library LibGameType {
    /// @notice Get the value of a `GameType` type in the form of the underlying uint8.
    /// @param _gametype The `GameType` type to get the value of.
    /// @return gametype_ The value of the `GameType` type as a uint8 type.
    function raw(GameType _gametype) internal pure returns (uint8 gametype_) {
        assembly {  
            gametype_ := _gametype
        }
    }
}
```
## Vulnerability Detail

The `raw` function is used at several places inside the codebase, usually while comparing two values. Comparison in the contract `OptimismPortal2` happens between `OptimismPortal2.respectedGameType` and `gameType` obtained from the game at a specific `_disputeGameIndex`.

### (GameType gameType,,) = disputeGameFactory.gameAtIndex(_disputeGameIndex)

The owner of the `DisputeGameFactory` can set an arbitrary number of game types by updating the array of game implementations:

```solidity
function setImplementation(GameType _gameType, IDisputeGame _impl) external onlyOwner {
    gameImpls[_gameType] = _impl;
    emit ImplementationSet(address(_impl), _gameType);
}
```

Once the implementation is set, the game can be created with a specific game type as we can see in `DisputeGameFactory::create`:
```solidity
    // Grab the implementation contract for the given `GameType`.
    IDisputeGame impl = gameImpls[_gameType];

    // If there is no implementation to clone for the given `GameType`, revert.
    if (address(impl) == address(0)) revert NoImplementation(_gameType);
```

Once the game is created it can be referenced by its `index` in the `OptimismPortal2::proveWithdrawalTransaction`.

### OptimismPortal2.respectedGameType

The variable `respectedGameType` in the portal contract can hold only one specific value (game type) at a time so only one type of game can be played. This value is firstly set in the constructor of the contract and can be later changed by the `guardian` role:
```solidity
function setRespectedGameType(GameType _gameType) external {
    require(msg.sender == guardian(), "OptimismPortal: only the guardian can set the respected game type");
    respectedGameType = _gameType;
    respectedGameTypeUpdatedAt = uint64(block.timestamp);
}
```

### Vulnerable scenario

The owner of the factory contract sets a new game type with its new implementation.
`GameType` is `uint32` and currently reserved numbers are: 0, 1, 255.

```solidity
library GameTypes {
    /// @dev A dispute game type the uses the cannon vm.
    GameType internal constant CANNON = GameType.wrap(0);

    /// @dev A permissioned dispute game type the uses the cannon vm.
    GameType internal constant PERMISSIONED_CANNON = GameType.wrap(1);

    /// @notice A dispute game type that uses an alphabet vm.
    ///         Not intended for production use.
    GameType internal constant ALPHABET = GameType.wrap(255);
}
```
For the new game, the owner chose the number `256` so users can create a game of type `256`. However, in the `OptimismPortal2` the `respectedGameType` is still set to `0` as the only game type allowed. 

Because the `LibGameType` function `raw` cast `uint32 -> uint8` the number `256` will overflow to `0`. It means the following comparison in the function `OptimismPortal2::proveWithdrawalTransaction` will pass:

```solidity
(GameType gameType,, IDisputeGame gameProxy) = disputeGameFactory.gameAtIndex(_disputeGameIndex);
Claim outputRoot = gameProxy.rootClaim();

// The game type of the dispute game must be the respected game type.
require(gameType.raw() == respectedGameType.raw(), "OptimismPortal: invalid game type");
```
## Impact
As was said in the introduction video, there will be more game types, but only one will be respected in the portal contract. This overflow may lead to more games being respected at the same time, which breaks the important invariant.

After reading the documentation I would expect that the type should be `uint8` so the owner should not register a `gameType > 255`. However, it is not checked in the code nor mentioned in the documentation so the vulnerable scenario is possible from the perspective of contest review, because the `gameType` is prone to overflow.

## Code Snippet

Library overflow:
https://github.com/sherlock-audit/2024-02-optimism-2024/blob/main/optimism/packages/contracts-bedrock/src/dispute/lib/LibUDT.sol#L123

Vulnerable `require`:
https://github.com/sherlock-audit/2024-02-optimism-2024/blob/main/optimism/packages/contracts-bedrock/src/L1/OptimismPortal2.sol#L261

## Tool used

Manual Review

## Recommendation
Change the return type of `LibGameType::raw` function to `uint32` to avoid unintended behavior. Or change the `GameType` to `uint8`.
